import "std/libc"
import "std/array"
import "std/map"
import "std/mem"

import "ast"
import "lex"
import "types"
import "module"
import "lita"
import "phase_result"
import "intern"

@asStr
public enum SymbolKind {
    TYPE,
    VAR,
    CONST,
    FUNC,
}

@asStr
public enum SymbolState {
    UNRESOLVED = 0,
    RESOLVING,
    RESOLVED,
    ERROR
}

public enum SymbolFlags {
    IS_LOCAL                = (1<<1),
    IS_FOREIGN              = (1<<2),
    IS_CONSTANT             = (1<<3),
    IS_USING                = (1<<4),
    IS_TYPE                 = (1<<5),
    IS_INCOMPLETE           = (1<<6),
    IS_GENERIC_TEMPLATE     = (1<<7),
    IS_BUILTIN              = (1<<8),
    IS_FROM_GENERIC_TEMPLATE= (1<<9),
    IS_IMPORTED             = (1<<10),
    IS_PUBLIC               = (1<<11),
    IS_EMITTED              = (1<<12),
    IS_TEST                 = (1<<13),
    IS_ALIAS                = (1<<14),
    IS_MAIN                 = (1<<15),
    IS_METHOD               = (1<<16),
    IS_TRAIT                = (1<<17),
    IS_TRAIT_METHOD         = (1<<18),
    IS_TRAIT_GENERATED      = (1<<19),
    IS_GENERATED            = (1<<20),
    IS_NOTE                 = (1<<21),

    // Marked for reset for incremental compliation
    IS_MARKED_RESET         = (1<<22),
    IS_HIDDEN               = (1<<23),
}

public const MAX_SYMBOL_NAME = 256;

public struct Symbol {
    kind: SymbolKind
    name: InternedString
    state: SymbolState
    decl: *Decl
    type: *TypeInfo

    usingParent: *Symbol
    declared: *Module
    usingModule: *Module
    flags: i32
    val: Value

    // TODO: put in struct
    genericCallsiteModule: *Module
    genericArgs: Array<*TypeInfo>
    genericParams: Array<GenericParam>
}

public func (this: *Symbol) isGenericCapable() : bool {
    return this.flags & SymbolFlags.IS_GENERIC_TEMPLATE ||
           this.flags & SymbolFlags.IS_FROM_GENERIC_TEMPLATE
}

public enum ScopeKind {
    MODULE,
    FUNC,
}

public struct ProgramSymbols {
    root: *Module
    values: Array<*Symbol>
    symbolTypes: Array<*Symbol>
    symbolFuncs: Array<*Symbol>
    mainEntry: *Symbol
    interfaceImpls: Map<Typeid, Array<Typeid>>
}

public struct Scope {
    kind: ScopeKind
    allocator: *const Allocator
    result: *PhaseResult
    parent: *Scope
    symbolTypes: Map<InternedString, *Symbol>
    symbolFuncs: Map<InternedString, *Symbol>

    module: *Module
}

public func NewScope(
    kind: ScopeKind,
    parent: *Scope,
    result: *PhaseResult,
    module: *Module,
    allocator: *const Allocator) : *Scope {

    var scope = new<Scope>(allocator)
    scope.init(kind, parent, result, module, allocator)
    return scope
}

public func (scope: *Scope) init(kind: ScopeKind,
                                 parent: *Scope,
                                 result: *PhaseResult,
                                 module: *Module,
                                 allocator: *const Allocator) {
    scope.kind = kind
    scope.allocator = allocator
    scope.parent = parent
    scope.result = result
    scope.module = module
    scope.symbolTypes = InternMap<*Symbol>(null, 16, allocator)
    scope.symbolFuncs = InternMap<*Symbol>(null, 16, allocator)
}

public func (scope: *Scope) initIncrementalBuild() {
    scope.markReset(scope.symbolTypes)
    scope.markReset(scope.symbolFuncs)
}

public func (scope: *Scope) postIncrementalBuild() {
    scope.cleanMarked(scope.symbolTypes)
    scope.cleanMarked(scope.symbolFuncs)
}

public func (s: *Scope) lookupFunc(name: InternedString) : *Symbol {
    var sym = s.symbolFuncs.get(name)
    if(sym) {
        return sym
    }

    if(!s.parent) {
        return null
    }

    return s.parent.lookupFunc(name)
}

public func (s: *Scope) lookup(name: InternedString, includeFuncs: bool = true) : *Symbol {
    var sym: *Symbol = null;

    if(includeFuncs) {
        sym = s.symbolFuncs.get(name)
        if(sym) {
            return sym
        }
    }

    sym = s.symbolTypes.get(name)
    if(sym) {
        return sym
    }

    if(!s.parent) {
        return null
    }

    return s.parent.lookup(name, includeFuncs)
}

/*
public func (s: *Scope) lookupByToken(name: Token) : *Symbol {
    if(name.type != TokenType.IDENTIFIER) {
        return null
    }

    return s.lookup(name.str)
}*/

public func (this: *Scope) importSymbol(name: InternedString,
                                        symbol: *Symbol) {

    var symbolStorage = &this.symbolFuncs

    if(symbol.kind != SymbolKind.FUNC) {
        symbolStorage = &this.symbolTypes
    }

    // if we are in the process of an incremental build, go ahead and
    // ignore duplicate checks..
    // if(symbol.flags & SymbolFlags.IS_MARKED_RESET) {
    //     symbol.flags |= ~SymbolFlags.IS_MARKED_RESET
    // }
    // else

    if(symbolStorage.contains(name) && !symbol.decl.hasNote("generated")) {
        var existingSym = symbolStorage.get(name)
        var decl = existingSym.decl
        this.result.addError(symbol.decl.startPos, "can't import symbol '%.*s' because it is already defined in %s:%d",
            name.length, name.buffer, decl.startPos.filename, decl.startPos.lineNumber)
        return;
    }

    symbolStorage.put(name, symbol)
}

public func (this: *Scope) addSymbol(name: InternedString,
                                     module: *Module,
                                     decl: *Decl,
                                     flags: i32) : *Symbol {

    var isFunc = decl.kind == StmtKind.FUNC_DECL
    var symbolStorage = &this.symbolTypes

    if(isFunc) {
        symbolStorage = &this.symbolFuncs
    }

    var sym: *Symbol = null;
    var isRebuild = this.module.flags & ModuleFlags.INCREMENTAL_COMPILATION;

    if(symbolStorage.contains(name)) {
        var isError = true

        // if we are incrementally rebuilding this module,
        // we will allow for recreating this existing symbol
        // if it's marked
        if(isRebuild) {
            sym = symbolStorage.get(name)
            if(sym.flags & SymbolFlags.IS_MARKED_RESET) {
                sym.flags &= ~SymbolFlags.IS_MARKED_RESET
                isError = false
            }
        }

        isError = isError && !decl.hasNote("generated")

        if(isError) {
            var existingSym = symbolStorage.get(name)
            var decl = existingSym.decl
            this.result.addError(decl.startPos, "symbol '%.*s' already defined in %s:%d",
                name.length, name.buffer, decl.startPos.filename, decl.startPos.lineNumber)
            return null;
        }
    }

    var isNewType = (decl.kind == StmtKind.FUNC_DECL   ||
                     decl.kind == StmtKind.STRUCT_DECL ||
                     decl.kind == StmtKind.UNION_DECL  ||
                     decl.kind == StmtKind.TRAIT_DECL  ||
                     decl.kind == StmtKind.TYPEDEF_DECL||
                     decl.kind == StmtKind.NOTE_DECL   ||
                     decl.kind == StmtKind.ENUM_DECL)

    if(this.kind == ScopeKind.FUNC) {
        if(!isNewType) {
            flags |= SymbolFlags.IS_LOCAL
        }
    }

    if(decl.hasNote("foreign")) {
        flags |= SymbolFlags.IS_FOREIGN
    }

    if(decl.hasNote("hidden")) {
        flags |= SymbolFlags.IS_HIDDEN
    }

    if(decl.attributes.isPublic) {
        flags |= SymbolFlags.IS_PUBLIC
    }

    if(isNewType) {
        flags |= SymbolFlags.IS_TYPE
    }

    var kind = SymbolKind.VAR
    switch(decl.kind) {
        case StmtKind.TYPEDEF_DECL: {
            flags |= SymbolFlags.IS_ALIAS
            // fallthru
        }
        case StmtKind.NOTE_DECL:
        case StmtKind.ENUM_DECL:
        case StmtKind.TRAIT_DECL:
        case StmtKind.UNION_DECL:
        case StmtKind.STRUCT_DECL: {
            kind = SymbolKind.TYPE

            if(decl.kind == StmtKind.TRAIT_DECL) {
                flags |= SymbolFlags.IS_TRAIT
            }

            if(decl.kind == StmtKind.NOTE_DECL) {
                flags |= SymbolFlags.IS_NOTE
                flags &= ~SymbolFlags.IS_TYPE
            }
            break;
        }
        case StmtKind.FUNC_DECL: {
            kind = SymbolKind.FUNC
            if(decl.hasNote("test")) {
                flags |= SymbolFlags.IS_TEST
            }

            var funcDecl = decl as (*FuncDecl)
            if(funcDecl.flags & FuncFlags.IS_METHOD) {
                flags |= SymbolFlags.IS_METHOD
            }
            break;
        }
        case StmtKind.CONST_DECL: {
            kind = SymbolKind.CONST
            flags |= SymbolFlags.IS_CONSTANT
            break;
        }
        default: {
            kind = SymbolKind.VAR
            break;
        }
    }

    if(!sym) {
        sym = new<Symbol>(this.allocator);
    }

    sym.kind = kind
    sym.name = name
    sym.state = SymbolState.UNRESOLVED
    sym.decl = decl
    sym.type = null
    sym.declared = module
    sym.flags = flags

    decl.sym = sym

    symbolStorage.put(sym.name, sym)
    return sym
}


// TEMP
public func (this: *Scope) print(header: *const char) {
    printf("Scope %s {\n", header)
    for(var i = 0; i < this.symbolTypes.capacity; i+=1) {
        var symName = this.symbolTypes.keys[i]
        var symbol = this.symbolTypes.values[i]
        if(!symName.addr || !symbol) {
            continue
        }
        printf("   '%.*s'\n", symbol.name.length, symbol.name.buffer)
    }

    for(var i = 0; i < this.symbolFuncs.capacity; i+=1) {
        var symName = this.symbolFuncs.keys[i]
        var symbol = this.symbolFuncs.values[i]
        if(!symName.addr || !symbol) {
            continue
        }
        printf("   '%.*s'\n", symbol.name.length, symbol.name.buffer)
    }
    printf("}\n")
}

func (this: *Scope) markReset(symbols: *Map<InternedString, *Symbol>) {
    for(var i = 0; i < symbols.capacity; i+=1) {
        var symName = symbols.keys[i]
        var symbol = symbols.values[i]
        if(!symName.addr || !symbol) {
            continue
        }

        // remove entries not declared in this module
        // so that they can be reimported if necessary
        if(!symbol.declared.equals(this.module)) {
            //printf("Not defined in module: %.*s\n", symName.length ,symName.buffer)
            symbols.keys[i] = EMPTY_STR
            symbols.values[i] = null
            symbols.length -= 1
            continue
        }

        symbol.flags |= SymbolFlags.IS_MARKED_RESET
    }
}

func (this: *Scope) cleanMarked(symbols: *Map<InternedString, *Symbol>) {
    for(var i = 0; i < symbols.capacity; i+=1) {
        var symName = symbols.keys[i]
        var symbol = symbols.values[i]
        if(!symName.addr || !symbol) {
            continue
        }

        if(!symbol.declared.equals(this.module)) {
            continue
        }

        // Remove any entries that haven't been stored again
        // this means they were deleted
        if(symbol.flags & SymbolFlags.IS_MARKED_RESET) {
            symbols.keys[i] = EMPTY_STR
            symbols.values[i] = null
            symbols.length -= 1


            var isFunc = symbol.kind == SymbolKind.FUNC

            // TODO: Reach out to all other modules who reference
            // this symbol and delete it...
            // TODO: performance...
            for(var i = 0; i < this.module.importedBy.size(); i+=1) {
                var m = this.module.importedBy.get(i).module
                if(isFunc) {
                    m.symbols.symbolFuncs.remove(symName)
                }
                else {
                    m.symbols.symbolTypes.remove(symName)
                }
            }
        }
    }
}