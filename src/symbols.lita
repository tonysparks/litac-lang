import "std/libc"
import "std/array"
import "std/map"
import "std/mem"
import "std/string/buffer"
import "std/string/builder"
import "std/string"
import "std/assert"
import "std/hash"

import "ast"
import "lex"
import "types"
import "module"
import "lita"
import "phase_result"
import "intern"
import "common"
import "coroutine"

@asStr
public enum SymbolKind {
    TYPE,
    VAR,
    FUNC,
}

@asStr
public enum SymbolState {
    UNRESOLVED = 0,
    RESOLVING,
    RESOLVED,
    ERROR
}

public enum SymbolFlags {
    // If this symbol was defined in function scope
    IS_LOCAL                = (1<<1),
    IS_FOREIGN              = (1<<2),
    IS_CONSTANT             = (1<<3),
    IS_USING                = (1<<4),
    IS_TYPE                 = (1<<5),
    IS_INCOMPLETE           = (1<<6),
    IS_GENERIC_TEMPLATE     = (1<<7),
    IS_BUILTIN              = (1<<8),
    IS_FROM_GENERIC_TEMPLATE= (1<<9),
    IS_IMPORTED             = (1<<10),
    IS_PUBLIC               = (1<<11),
    IS_INTERNAL             = (1<<12),
    IS_PRIVATE              = (1<<13),
    IS_EMITTED              = (1<<14),
    IS_TEST                 = (1<<15),
    IS_ALIAS                = (1<<16),
    IS_MAIN                 = (1<<17),
    IS_METHOD               = (1<<18),
    IS_TRAIT                = (1<<19),
    IS_TRAIT_METHOD         = (1<<20),
    IS_TRAIT_GENERATED      = (1<<21),
    IS_GENERATED            = (1<<22),
    IS_NOTE                 = (1<<23),

    // Marked for reset for incremental compliation
    IS_MARKED_RESET         = (1<<24),
    IS_HIDDEN               = (1<<25),
    IS_FROM_PREPROCESSOR    = (1<<26),

    IS_COROUTINE            = (1<<27),

}

public struct SymGenericArg {
    kind: GenericArgKind
    type: *TypeInfo
}

public const MAX_SYMBOL_NAME = 256;

public struct Symbol {
    kind: SymbolKind

    name: InternedString
    qualifiedName: InternedString

    state: SymbolState
    decl: *Decl
    type: *TypeInfo

    usingParent: *Symbol
    declared: *Module
    usingModule: *Module
    flags: i32
    val: Value

    coroutine: *CoroutineStack

    // TODO: put in struct
    genericCallsiteModule: *Module
    genericArgs: Array<SymGenericArg>
    genericParams: Array<GenericParam>
}

public func (this: *Symbol) isAliased() : bool {
    if(!this) {
        return false
    }

    // the type of this symbol
    var typeInfo = this.type
    if(!typeInfo) {
        return false
    }

    // now see if the symbol is the same
    return typeInfo.sym != this
}

public func (this: *Symbol) isGenericCapable() : bool {
    return this.flags & SymbolFlags.IS_GENERIC_TEMPLATE ||
           this.flags & SymbolFlags.IS_FROM_GENERIC_TEMPLATE
}

public func (this: *Symbol) isVisibleTo(module: *Module) : bool {
    if(this.declared == module) {
        return true
    }

    if(this.flags & SymbolFlags.IS_PRIVATE) {
        return false
    }

    if(this.flags & SymbolFlags.IS_PUBLIC) {
        return true
    }

    return this.declared.isInPackage(module)
}

public func (this: *Symbol) hash() : u32 {
    var hash = ((this as (usize)) % 4294967291_u32) as (u32)
    return hash;
}

public func (this: *Symbol) equals(other: *Symbol) : bool {
    return this == other
}

public enum ScopeKind {
    MODULE,
    FUNC,
}

public struct ProgramSymbols {
    root: *Module
    values: Array<*Symbol>
    symbolTypes: Array<*Symbol>
    symbolFuncs: Array<*Symbol>
    mainEntry: *Symbol
    interfaceImpls: Map<Typeid, Array<Typeid>>
}

public struct Scope {
    kind: ScopeKind
    allocator: *const Allocator
    result: *PhaseResult
    parent: *Scope
    symbolNotes: Map<InternedString, *Symbol>
    symbolTypes: Map<InternedString, *Symbol>
    symbolFuncs: Map<InternedString, *Symbol>

    module: *Module
}

public func NewScope(
    kind: ScopeKind,
    parent: *Scope,
    result: *PhaseResult,
    module: *Module,
    allocator: *const Allocator) : *Scope {

    var scope = new<Scope>(allocator)
    scope.init(kind, parent, result, module, allocator)
    return scope
}

public func (scope: *Scope) init(kind: ScopeKind,
                                 parent: *Scope,
                                 result: *PhaseResult,
                                 module: *Module,
                                 allocator: *const Allocator) {
    scope.kind = kind
    scope.allocator = allocator
    scope.parent = parent
    scope.result = result
    scope.module = module
    scope.symbolTypes = InternMap<*Symbol>(null, 16, allocator)

    // we can only declare notes and functions at the module
    // level
    if(kind == ScopeKind.MODULE) {
        scope.symbolFuncs = InternMap<*Symbol>(null, 32, allocator)
        scope.symbolNotes = InternMap<*Symbol>(null, 16, allocator)
    }
    else {
        scope.symbolFuncs = Map<InternedString, *Symbol>{}
        scope.symbolNotes = Map<InternedString, *Symbol>{}
    }
}

public func (scope: *Scope) initIncrementalBuild() {
    scope.markReset(&scope.symbolTypes)
    scope.markReset(&scope.symbolFuncs)
    scope.markReset(&scope.symbolNotes)
}

public func (scope: *Scope) postIncrementalBuild() {
    //scope.cleanMarked(&scope.symbolTypes)
    //scope.cleanMarked(&scope.symbolFuncs)
}

public func (s: *Scope) lookupFunc(name: InternedString) : *Symbol {
    var sym = s.symbolFuncs.get(name)
    if(sym && !(sym.flags & SymbolFlags.IS_MARKED_RESET)) {
        return sym
    }

    if(!s.parent) {
        return null
    }

    return s.parent.lookupFunc(name)
}

public func (s: *Scope) lookupNote(name: InternedString) : *Symbol {
    var sym = s.symbolNotes.get(name)
    if(sym && !(sym.flags & SymbolFlags.IS_MARKED_RESET)) {
        return sym
    }

    if(!s.parent) {
        return null
    }

    return s.parent.lookupNote(name)
}

public func (s: *Scope) lookup(name: InternedString, includeFuncs: bool = true) : *Symbol {
    var sym: *Symbol = null;

    if(includeFuncs) {
        sym = s.symbolFuncs.get(name)
        if(sym && !(sym.flags & SymbolFlags.IS_MARKED_RESET)) {
            return sym
        }
    }

    sym = s.symbolTypes.get(name)
    if(sym && !(sym.flags & SymbolFlags.IS_MARKED_RESET)) {
        return sym
    }

    if(!s.parent) {
        return null
    }

    return s.parent.lookup(name, includeFuncs)
}

public func (this: *Scope) importSymbolWithAlias(importSrcPos: SrcPos,
                                                 name: InternedString,
                                                 alias: InternedString,
                                                 symbol: *Symbol) {

    var buffer = [MAX_SYMBOL_NAME]char{};

    var fullName = StringBufferInit(buffer, MAX_SYMBOL_NAME, 0)
    fullName.appendStrn(alias.buffer, alias.length)
    fullName.appendStrn("::", 2)
    fullName.appendStrn(name.buffer, name.length)

    var nameIntern = this.module.lita.strings.internCopy(fullName.cStrConst(), fullName.length)
    this.importSymbol(importSrcPos, nameIntern, symbol)
}


public func (this: *Scope) importSymbol(importSrcPos: SrcPos,
                                        name: InternedString,
                                        symbol: *Symbol) {

    var symbolStorage = &this.symbolTypes;
    if(symbol.kind == SymbolKind.FUNC) {
        symbolStorage = &this.symbolFuncs
    }
    else if(symbol.flags & SymbolFlags.IS_NOTE) {
        symbolStorage = &this.symbolNotes
    }

    if(symbolStorage.contains(name)) {
        if(symbol.flags & SymbolFlags.IS_GENERATED) {

            // allow presedence to be set
            var isStub = symbol.decl.getNoteArgument("generated", "stub")
            if(isStub && isStub.argExpr && isStub.argExpr.getKind() == StmtKind.BOOLEAN_EXPR) {
                var boolExpr = isStub.argExpr as (*BooleanExpr)

                if(boolExpr.boolean) {
                    return;
                }
            }
        }
        else {
            var existingSym = symbolStorage.get(name)

            // if this symbol was previously defined and we're doing
            // an incremental build, go ahead with the import -- if/when
            // the symbol gets a declaration in this module, it will error
            // there
            if(!(existingSym.flags & SymbolFlags.IS_MARKED_RESET)) {
                var decl = existingSym.decl

                var existingStartPos = decl.getStartPos()
                var newStartPos = symbol.decl.getStartPos()

                this.result.addError(importSrcPos,
                    "can't import symbol '%.*s' defined in '%s:%d' because it is already defined in '%s:%d'",
                    name.length, name.buffer,
                    newStartPos.filename, newStartPos.lineNumber,
                    existingStartPos.filename, existingStartPos.lineNumber
                )
                return;
            }
        }
    }

    symbolStorage.put(name, symbol);
}

public func (this: *Scope) removeSymbol(symbol: *Symbol) {
    var symbolStorage = &this.symbolTypes;
    if(symbol.kind == SymbolKind.FUNC) {
        symbolStorage = &this.symbolFuncs
    }
    else if(symbol.flags & SymbolFlags.IS_NOTE) {
        symbolStorage = &this.symbolNotes
    }

    // TODO: Performance!
    for(var it = symbolStorage.iter(); it.hasNext();) {
        var entry = it.next()

        if(entry.value == symbol) {
            symbolStorage.remove(entry.key)
            return;
        }
    }
}

public func (this: *Scope) addSymbol(name: InternedString,
                                     module: *Module,
                                     decl: *Decl,
                                     flags: i32) : *Symbol {

    var symbolStorage = &this.symbolTypes

    var declKind = decl.asNode().kind
    switch(declKind) {
        case StmtKind.FUNC_DECL:
            symbolStorage = &this.symbolFuncs
            break;
        case StmtKind.NOTE_DECL:
            symbolStorage = &this.symbolNotes
            break;
        default: // do nothing
    }

    var sym: *Symbol = null;
    var isRebuild = this.module.flags & ModuleFlags.INCREMENTAL_COMPILATION;

    if(symbolStorage.contains(name)) {
        var isError = !(this.module.flags & ModuleFlags.IGNORE_DUPLICATE_SYMBOLS);

        // if we are incrementally rebuilding this module,
        // we will allow for recreating this existing symbol
        // if it's marked
        if(isRebuild) {
            sym = symbolStorage.get(name)
            if(sym.flags & SymbolFlags.IS_MARKED_RESET) {
                sym.flags &= ~SymbolFlags.IS_MARKED_RESET
                isError = false
            }
            else if(sym.flags & SymbolFlags.IS_FROM_GENERIC_TEMPLATE) {
                var existingSym = symbolStorage.get(name)
                var decl = existingSym.decl
                if(decl == null) {
                    isError = false
                }
            }
        }

        // We allow for redefining annotated generated
        // types, as this types can be redefined by
        // the preprocessor or the compiler
        if(decl.hasNote("generated")) {
            isError = false
        }

        if(isError) {
            var existingSym = symbolStorage.get(name)
            var startPos = existingSym.decl.getStartPos()
            this.result.addError(startPos,
                "symbol '%.*s' already defined in %s:%d",
                name.length, name.buffer,
                startPos.filename,
                startPos.lineNumber
            )
            return null;
        }
    }

    var isNewType = (declKind == StmtKind.FUNC_DECL   ||
                     declKind == StmtKind.STRUCT_DECL ||
                     declKind == StmtKind.UNION_DECL  ||
                     declKind == StmtKind.TRAIT_DECL  ||
                     declKind == StmtKind.TYPEDEF_DECL||
                     declKind == StmtKind.NOTE_DECL   ||
                     declKind == StmtKind.ENUM_DECL)

    if(this.kind == ScopeKind.FUNC) {
        if(!isNewType) {
            flags |= SymbolFlags.IS_LOCAL
        }
    }

    if(decl.hasNote("foreign")) {
        flags |= SymbolFlags.IS_FOREIGN
    }

    if(decl.hasNote("generated")) {
        flags |= SymbolFlags.IS_GENERATED
    }

    if(decl.hasNote("hidden")) {
        flags |= SymbolFlags.IS_HIDDEN
    }

    switch(decl.attributes.visibility) {
        case Visibility.PUBLIC:
            flags |= SymbolFlags.IS_PUBLIC
            break;
        case Visibility.INTERNAL:
            flags |= SymbolFlags.IS_INTERNAL
            break;
        default:
            flags |= SymbolFlags.IS_PRIVATE
    }

    var qualifiedName = name
    if(isNewType) {
        flags |= SymbolFlags.IS_TYPE

        if(!(flags & SymbolFlags.IS_BUILTIN)) {
            qualifiedName = this.createQualifiedName(name)
        }
    }

    var kind = SymbolKind.VAR
    switch(declKind) {
        case StmtKind.TYPEDEF_DECL: {
            flags |= SymbolFlags.IS_ALIAS
            // fallthru
        }
        case StmtKind.NOTE_DECL:
        case StmtKind.ENUM_DECL:
        case StmtKind.TRAIT_DECL:
        case StmtKind.UNION_DECL:
        case StmtKind.STRUCT_DECL: {
            kind = SymbolKind.TYPE

            if(declKind == StmtKind.TRAIT_DECL) {
                flags |= SymbolFlags.IS_TRAIT
            }

            if(declKind == StmtKind.NOTE_DECL) {
                flags |= SymbolFlags.IS_NOTE
                flags &= ~SymbolFlags.IS_TYPE
            }
            break;
        }
        case StmtKind.FUNC_DECL: {
            kind = SymbolKind.FUNC
            if(decl.hasNote("test")) {
                flags |= SymbolFlags.IS_TEST
            }
            if(decl.hasNote("suspend")) {
                flags |= SymbolFlags.IS_COROUTINE
            }

            var funcDecl = decl as (*FuncDecl)
            if(funcDecl.flags & FuncFlags.IS_METHOD) {
                flags |= SymbolFlags.IS_METHOD
            }
            // TODO: Revisit if this should be a keyword or
            // a note
            if(funcDecl.flags & FuncFlags.IS_COROUTINE) {
                flags |= SymbolFlags.IS_COROUTINE
            }
            break;
        }
        case StmtKind.CONST_DECL: {
            flags |= SymbolFlags.IS_CONSTANT
            /* fallthru */
        }
        default: {
            kind = SymbolKind.VAR
            break;
        }
    }

    var isNewSym = sym == null
    if(isNewSym) {
        sym = new<Symbol>(this.allocator);
        sym.type = null
        sym.coroutine = null

        // TODO: Shouldn't this be only for func types?
        if(kind == SymbolKind.FUNC) {
            sym.coroutine = new<CoroutineStack>(this.allocator);
            sym.coroutine.init(this.allocator)
        }
    }

    sym.kind = kind
    sym.name = name
    sym.qualifiedName = qualifiedName
    sym.state = SymbolState.UNRESOLVED
    sym.decl = decl
    sym.declared = module
    sym.flags = flags

    decl.sym = sym

    if(isNewSym) {
        symbolStorage.put(sym.name, sym)
    }
    return sym
}

public func (this: *Scope) createQualifiedName(name: InternedString) : InternedString {
    var buffer:[MAX_SYMBOL_NAME]char;
    var qualifiedNameBuffer = StringBufferInit(buffer, MAX_SYMBOL_NAME, 0)

    for(var i = 0; i < name.length; i+=1) {
        var c = name.buffer[i]
        if(c == ':') {
            return name
        }
        if(c == '<') {
            break
        }
    }

    qualifiedNameBuffer.append("%.*s::%.*s",
        this.module.id.packageName.length,
        this.module.id.packageName.buffer,
        name.length,
        name.buffer
    )

    var qualifiedName = this.module.lita.strings.internCopy(
        qualifiedNameBuffer.buffer,
        qualifiedNameBuffer.length
    )

    return qualifiedName
}

// TEMP
public func (this: *Scope) print(header: *const char) {
    printf("Scope %s {\n", header)
    for(var it = this.symbolTypes.iter(); it.hasNext();) {
        var symbol = it.next().value;
        printf("   '%.*s'\n", symbol.name.length, symbol.name.buffer)
    }

    for(var it = this.symbolFuncs.iter(); it.hasNext();) {
        var symbol = it.next().value;
        printf("   '%.*s'\n", symbol.name.length, symbol.name.buffer)
    }
    printf("}\n")
}

func (this: *Scope) markReset(symbols: *Map<InternedString, *Symbol>) {
    for(var it = symbols.iter(); it.hasNext();) {
        var entry = it.next()
        var symbol = entry.value;

        var isGenericFromThisModule = symbol.genericCallsiteModule != null
                                   && symbol.genericCallsiteModule.equals(this.module)

        // remove entries not declared in this module
        // so that they can be reimported if necessary
        if(!isGenericFromThisModule && !symbol.declared.equals(this.module)) {
            it.remove()
            continue
        }

        if(symbol.flags & SymbolFlags.IS_FROM_PREPROCESSOR) {
            this.removeSymbolFromModules(symbol)
            it.remove()
            continue
        }

        symbol.flags |= SymbolFlags.IS_MARKED_RESET
        symbol.state = SymbolState.UNRESOLVED
        symbol.decl = null
        // DO NOT clear symbol.type, this is used as a cache and allows
        // for type look up and equality
        // symbol.type = null
    }
}

func (this: *Scope) cleanMarked(symbols: *Map<InternedString, *Symbol>, remove: bool = false) {
    for(var it = symbols.iter(); it.hasNext();) {
        var entry = it.next()
        var symbol = entry.value;

        var isGenericFromThisModule = symbol.genericCallsiteModule != null
                                   && symbol.genericCallsiteModule.equals(this.module)


        if(!isGenericFromThisModule && !symbol.declared.equals(this.module)) {
            continue
        }

        // Remove any entries that haven't been stored again
        // this means they were deleted
        if(symbol.flags & SymbolFlags.IS_MARKED_RESET) {
            this.removeSymbolFromModules(symbol)

            // We may not want to remove it from this scope's
            // cache, as the symbol may be re-added at which point
            // the symbol should be re-used along with the TypeInfo.
            if(remove) {
                it.remove()
            }
        }
    }
}

func (this: *Scope) removeSymbolFromModules(symbol: *Symbol) {
    for(var it = this.module.importedBy.iter(); it.hasNext();) {
        var entry = it.next()
        var m = entry.value.module
        m.symbols.removeSymbol(symbol)
    }
}