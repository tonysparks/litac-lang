import "std/libc"
import "std/array"
import "std/map"
import "std/mem"

import "ast"
import "lex"
import "types"
import "module"
import "lita"
import "phase_result"
import "intern"

@asStr
public enum SymbolKind {
    TYPE,
    VAR,
    CONST,
    FUNC,
}

@asStr
public enum SymbolState {
    UNRESOLVED = 0,
    RESOLVING,
    RESOLVED,
    ERROR
}

public enum SymbolFlags {
    IS_LOCAL                = (1<<1),
    IS_FOREIGN              = (1<<2),
    IS_CONSTANT             = (1<<3),
    IS_USING                = (1<<4),
    IS_TYPE                 = (1<<5),
    IS_INCOMPLETE           = (1<<6),
    IS_GENERIC_TEMPLATE     = (1<<7),
    IS_BUILTIN              = (1<<8),
    IS_FROM_GENERIC_TEMPLATE= (1<<9),
    IS_IMPORTED             = (1<<10),
    IS_PUBLIC               = (1<<11),
    IS_EMITTED              = (1<<12),
    IS_TEST                 = (1<<13),
    IS_ALIAS                = (1<<14),
    IS_MAIN                 = (1<<15),
    IS_METHOD               = (1<<16),
    IS_TRAIT                = (1<<17),
    IS_TRAIT_METHOD         = (1<<18),
    IS_TRAIT_GENERATED      = (1<<19),
    IS_GENERATED            = (1<<20),
    IS_NOTE                 = (1<<21),
}

public const MAX_SYMBOL_NAME = 256;

public struct Symbol {
    kind: SymbolKind
    name: InternedString
    state: SymbolState
    decl: *Decl
    type: *TypeInfo

    usingParent: *Symbol
    declared: *Module
    usingModule: *Module
    flags: i32
    val: Value

    // TODO: put in struct
    genericCallsiteModule: *Module
    genericArgs: Array<*TypeInfo>
    genericParams: Array<GenericParam>
}

public func (this: *Symbol) isGenericCapable() : bool {
    return this.flags & SymbolFlags.IS_GENERIC_TEMPLATE ||
           this.flags & SymbolFlags.IS_FROM_GENERIC_TEMPLATE
}

public enum ScopeKind {
    MODULE,
    FUNC,
}

public struct ProgramSymbols {
    root: *Module
    values: Array<*Symbol>
    symbolTypes: Array<*Symbol>
    symbolFuncs: Array<*Symbol>
    mainEntry: *Symbol
    interfaceImpls: Map<Typeid, Array<Typeid>>
}

public struct Scope {
    kind: ScopeKind
    allocator: *const Allocator
    result: *PhaseResult
    parent: *Scope
    symbolTypes: Map<InternedString, *Symbol>
    symbolFuncs: Map<InternedString, *Symbol>

    module: *Module // TEMP
}

public func NewScope(kind: ScopeKind, parent: *Scope, result: *PhaseResult, allocator: *const Allocator) : *Scope {
    var scope = new<Scope>(allocator)
    scope.init(kind, parent, result, allocator)
    return scope
}

public func (scope: *Scope) init(kind: ScopeKind,
                                 parent: *Scope,
                                 result: *PhaseResult,
                                 allocator: *const Allocator) {
    scope.kind = kind
    scope.allocator = allocator
    scope.parent = parent
    scope.result = result
    scope.symbolTypes = InternMap<*Symbol>(null, 16, allocator)
    scope.symbolFuncs = InternMap<*Symbol>(null, 16, allocator)
}

public func (s: *Scope) lookupFunc(name: InternedString) : *Symbol {
    var sym = s.symbolFuncs.get(name)
    if(sym) {
        return sym
    }

    if(!s.parent) {
        return null
    }

    return s.parent.lookupFunc(name)
}

public func (s: *Scope) lookup(name: InternedString, includeFuncs: bool = true) : *Symbol {
    var sym: *Symbol = null;

    if(includeFuncs) {
        sym = s.symbolFuncs.get(name)
        if(sym) {
            return sym
        }
    }

    sym = s.symbolTypes.get(name)
    if(sym) {
        return sym
    }

    if(!s.parent) {
        return null
    }

    return s.parent.lookup(name, includeFuncs)
}

/*
public func (s: *Scope) lookupByToken(name: Token) : *Symbol {
    if(name.type != TokenType.IDENTIFIER) {
        return null
    }

    return s.lookup(name.str)
}*/

public func (this: *Scope) importSymbol(name: InternedString,
                                        symbol: *Symbol) {

    if(symbol.kind == SymbolKind.FUNC) {
        if(this.symbolFuncs.contains(name) && !symbol.decl.hasNote("generated")) {
            var existingSym = this.symbolFuncs.get(name)
            var decl = existingSym.decl
            this.result.addError(symbol.decl.startPos, "can't import symbol '%.*s' because it is already defined in %s:%d",
                name.length, name.buffer, decl.startPos.filename, decl.startPos.lineNumber)
            return;
        }

        this.symbolFuncs.put(name, symbol)
    }
    else {
        if(this.symbolTypes.contains(name) && !symbol.decl.hasNote("generated")) {
            var existingSym = this.symbolTypes.get(name)
            var decl = existingSym.decl
            this.result.addError(symbol.decl.startPos, "can't import symbol '%.*s' because it is already defined in %s:%d",
                name.length, name.buffer, decl.startPos.filename, decl.startPos.lineNumber)
            return;
        }

        this.symbolTypes.put(name, symbol)
    }
}

public func (this: *Scope) addSymbol(name: InternedString,
                                     module: *Module,
                                     decl: *Decl,
                                     flags: i32) : *Symbol {

    var isFunc = decl.kind == StmtKind.FUNC_DECL

    if(isFunc) {
        if(this.symbolFuncs.contains(name) && !decl.hasNote("generated")) {
            var existingSym = this.symbolFuncs.get(name)
            var decl = existingSym.decl
            this.result.addError(decl.startPos, "symbol '%.*s' already defined in %s:%d",
                name.length, name.buffer, decl.startPos.filename, decl.startPos.lineNumber)
            return null;
        }
    }
    else {
        if(this.symbolTypes.contains(name) && !decl.hasNote("generated")) {
            var existingSym = this.symbolTypes.get(name)
            var decl = existingSym.decl
            this.result.addError(decl.startPos, "symbol '%.*s' already defined in %s:%d",
                name.length, name.buffer, decl.startPos.filename, decl.startPos.lineNumber)
            return null;
        }
    }

    var isNewType = (decl.kind == StmtKind.FUNC_DECL   ||
                     decl.kind == StmtKind.STRUCT_DECL ||
                     decl.kind == StmtKind.UNION_DECL  ||
                     decl.kind == StmtKind.TRAIT_DECL  ||
                     decl.kind == StmtKind.TYPEDEF_DECL||
                     decl.kind == StmtKind.NOTE_DECL   ||
                     decl.kind == StmtKind.ENUM_DECL)

    if(this.kind == ScopeKind.FUNC) {
        if(!isNewType) {
            flags |= SymbolFlags.IS_LOCAL
        }
    }

    if(decl.hasNote("foreign")) {
        flags |= SymbolFlags.IS_FOREIGN
    }

    if(decl.attributes.isPublic) {
        flags |= SymbolFlags.IS_PUBLIC
    }

    if(isNewType) {
        flags |= SymbolFlags.IS_TYPE
    }

    var kind = SymbolKind.VAR
    switch(decl.kind) {
        case StmtKind.TYPEDEF_DECL: {
            flags |= SymbolFlags.IS_ALIAS
            // fallthru
        }
        case StmtKind.NOTE_DECL:
        case StmtKind.ENUM_DECL:
        case StmtKind.TRAIT_DECL:
        case StmtKind.UNION_DECL:
        case StmtKind.STRUCT_DECL: {
            kind = SymbolKind.TYPE

            if(decl.kind == StmtKind.TRAIT_DECL) {
                flags |= SymbolFlags.IS_TRAIT
            }

            if(decl.kind == StmtKind.NOTE_DECL) {
                flags |= SymbolFlags.IS_NOTE
                flags &= ~SymbolFlags.IS_TYPE
            }
            break;
        }
        case StmtKind.FUNC_DECL: {
            kind = SymbolKind.FUNC
            if(decl.hasNote("test")) {
                flags |= SymbolFlags.IS_TEST
            }

            var funcDecl = decl as (*FuncDecl)
            if(funcDecl.flags & FuncFlags.IS_METHOD) {
                flags |= SymbolFlags.IS_METHOD
            }
            break;
        }
        case StmtKind.CONST_DECL: {
            kind = SymbolKind.CONST
            flags |= SymbolFlags.IS_CONSTANT
            break;
        }
        default: {
            kind = SymbolKind.VAR
            break;
        }
    }

    var sym = new<Symbol>(this.allocator)
    sym.kind = kind
    sym.name = name
    sym.state = SymbolState.UNRESOLVED
    sym.decl = decl
    sym.type = null
    sym.declared = module
    sym.flags = flags

    decl.sym = sym

    if(isFunc) {
        this.symbolFuncs.put(sym.name, sym)
    }
    else {
        this.symbolTypes.put(sym.name, sym)
    }

    return sym
}


// TEMP
public func (this: *Scope) print(header: *const char) {
    printf("Scope %s {\n", header)
    for(var i = 0; i < this.symbolTypes.capacity; i+=1) {
        var symName = this.symbolTypes.keys[i]
        var symbol = this.symbolTypes.values[i]
        if(!symName.addr || !symbol) {
            continue
        }
        printf("   '%.*s'\n", symbol.name.length, symbol.name.buffer)
    }

    for(var i = 0; i < this.symbolFuncs.capacity; i+=1) {
        var symName = this.symbolFuncs.keys[i]
        var symbol = this.symbolFuncs.values[i]
        if(!symName.addr || !symbol) {
            continue
        }
        printf("   '%.*s'\n", symbol.name.length, symbol.name.buffer)
    }
    printf("}\n")
}