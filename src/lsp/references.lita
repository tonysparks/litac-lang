import "std/libc"
import "std/assert"
import "std/json"
import "std/map"
import "std/array"
import "std/mem"

import "ast"
import "common"
import "lita"
import "lex"
import "parser"
import "types"
import "symbols"

import "lsp/workspace"
import "lsp/protocol"
import "lsp/lsp"
import "lsp/util"

public func FindTokenByPosition(lsp: *LspServer, alloc: *const Allocator, doc: *TextDocument, pos: Position) : Token {
    var lex = LexerInit(doc.filename, doc.text.cStr(), alloc)

    var targetLine = pos.line + 1
    lsp.log("Searching for token at: %d:%d\n", pos.line, pos.character)
    while(!lex.eof()) {
        var token = lex.nextToken()
        if(token.pos.lineNumber == targetLine) {
            do {
                var len = (token.pos.end - token.pos.start) as (i32)
                if(token.pos.position >= pos.character &&
                   pos.character <=  token.pos.position + len) {
                       lsp.log("Found token: '%s'\n", token.asString())
                    return token
                }

                token = lex.nextToken()
            }
            while(!lex.eof() && token.pos.lineNumber == targetLine);
            break;
        }
    }
    lsp.log("No token found!\n")
    return lex.eofToken()
}

public struct Reference {
    type: Typeid
    pos: SrcPos
}

struct FieldReference {
    /* the parent structure typeid this fields belongs to */
    parent: Typeid
    /* the offset into the structure where the field is */
    offset: i32
    /* the source position */
    pos: SrcPos
}

public struct ReferenceDatabase {
    allocator: *const Allocator
    typeReferences: Array<Reference>
    fieldReferences: Array<FieldReference>
    symbols: Map<usize, Array<SrcPos>>
}


public func (this: *ReferenceDatabase) init(allocator: *const Allocator) {
    this.allocator = allocator
    this.typeReferences.init(1024, allocator)
    this.fieldReferences.init(1024, allocator)
    this.symbols.init(Array<SrcPos>{}, 1024, PtrHashFn<usize>, PtrEqualFn<usize>, allocator)
}

public func (this: *ReferenceDatabase) addSymbolReference(symbol: *Symbol, pos: SrcPos) {
    if(!symbol) return;

    var ptr = symbol as (usize)
    if(!this.symbols.contains(ptr)) {
        var locations = ArrayInit<SrcPos>(16, this.allocator)
        this.symbols.put(ptr, locations)
    }

    var locations = this.symbols.getPtr(ptr)
    locations.add(pos)
}


public func (this: *ReferenceDatabase) addTypeReference(type: *TypeSpec) {
    if(!type) return;

    var base = type.getBaseType()
    if(!base) return;

    this.addTypeReferenceByTypeInfo(type.pos, base.typeInfo)
}

public func (this: *ReferenceDatabase) addTypeReferenceBySymbol(srcPos: SrcPos, sym: *Symbol) {
    if(sym && sym.type) {
        this.addTypeReferenceByTypeInfo(srcPos, sym.type)
    }
}

public func (this: *ReferenceDatabase) addTypeReferenceByTypeInfo(srcPos: SrcPos, typeInfo: *TypeInfo) {
    if(!typeInfo) return;

    typeInfo = typeInfo.getBaseType()

    // if this is a generic type, make sure to add references
    // to the base generic template - this allows for the generic template itself
    // to look up references
    if(IsGenericCapable(typeInfo)) {
        if(typeInfo.sym && typeInfo.sym.flags & SymbolFlags.IS_FROM_GENERIC_TEMPLATE) {
            var genInfo = typeInfo as (*GenericTypeInfo)
            this.typeReferences.add(Reference {
                .type = genInfo.genericTypeid,
                .pos = srcPos
            })
        }
    }

    this.typeReferences.add(Reference {
        .type = typeInfo.typeid,
        .pos = srcPos
    })
}

public func (this: *ReferenceDatabase) addFieldReference(srcPos: SrcPos, parent: *TypeInfo, offset: i32) {
    if(!parent) return;

    this.fieldReferences.add(FieldReference {
        .parent = parent.typeid,
        .offset = offset,
        .pos = srcPos
    })
}

public func (this: *ReferenceDatabase) getSymbolReferences(symbol: *Symbol) : Array<SrcPos> {
    if(!symbol) return Array<SrcPos>{}

    var ptr = symbol as (usize)
    if(this.symbols.contains(ptr)) {
        var result = this.symbols.get(ptr)
        return result
    }

    return Array<SrcPos>{}
}


public func (this: *ReferenceDatabase) getTypeReferences(typeid: Typeid, results: *Array<SrcPos>) {
    for(var i = 0; i < this.typeReferences.size(); i += 1) {
        var ref = this.typeReferences.getPtr(i)
        if(ref.type == typeid) {
            results.add(ref.pos)
        }
    }
}

public func (this: *ReferenceDatabase) getFieldReferences(typeid: Typeid, offset: i32, results: *Array<SrcPos>) {
    for(var i = 0; i < this.fieldReferences.size(); i += 1) {
        var ref = this.fieldReferences.getPtr(i)
        if(ref.parent == typeid && ref.offset == offset) {
            results.add(ref.pos)
        }
    }
}

