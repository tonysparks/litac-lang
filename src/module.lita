import "std/array"
import "std/map"
import "std/string"
import "std/string_view"
import "std/string_buffer"
import "std/system"
import "std/libc"
import "std/mem"

import "phase_result"
import "symbols"
import "lita"
import "types"
import "ast"
import "ast_new"
import "lex"

public enum ModuleFlags {
    TYPE_CHECKED  = (1<<0),
    TYPE_RESOLVED = (1<<1),
    TYPE_IMPORTED = (1<<2),
}

public struct ModuleId {
    filename: [MAX_PATH]char   // full path the file of this module
    packageName: StringView    // relative path from the main module's directory
    name: StringView           // the name of the module
}

public struct ModuleImport {
    module: *Module
    alias: *StringView
    isUsing: bool
}

public struct Module {
    id: ModuleId        // TODO: don't make a separate allocation
    text: *char         // text of modules file
    ast: *ModuleStmt
    symbols: Scope
    currentScope: *Scope
    genericSymbols: *Map<*const char, *Symbol>
    flags: i32

    importedBy: Array<ModuleImport> // modules that import this module

    allocator: *const Allocator
}

public const BUILTIN_IMPORT_TOKEN = Token {
    .type = TokenType.STRING,
    .value = Value {
        .str = StringView {
            .buffer = "builtin",
            .length = 7
        }
    }
}
public const builtins = Module{}
public func BuiltinsInit(lita: *Lita) : *Module {
    builtins.text = null
    builtins.ast = null
    builtins.flags = 0
    builtins.allocator = lita.allocator
    builtins.importedBy.init(512, lita.allocator)
    builtins.symbols.init(ScopeKind.MODULE, null, lita.result, lita.allocator)
    builtins.genericSymbols = &lita.genericSymbols

    var filename: [MAX_PATH]char;
    var pathStr = StringInit(filename, MAX_PATH);
    pathStr.format("%s/stdlib/std/builtins.lita", lita.options.litaPath)
    builtins.id.fromFile(lita, pathStr.cStr())

    AddBuiltin(lita, &BOOL_TYPE)
    AddBuiltin(lita, &CHAR_TYPE)
    AddBuiltin(lita, &I8_TYPE)
    AddBuiltin(lita, &U8_TYPE)
    AddBuiltin(lita, &I16_TYPE)
    AddBuiltin(lita, &U16_TYPE)
    AddBuiltin(lita, &I32_TYPE)
    AddBuiltin(lita, &U32_TYPE)
    AddBuiltin(lita, &I64_TYPE)
    AddBuiltin(lita, &U64_TYPE)
    AddBuiltin(lita, &F32_TYPE)
    AddBuiltin(lita, &F64_TYPE)
    AddBuiltin(lita, &USIZE_TYPE)
    AddBuiltin(lita, &NULL_TYPE)
    AddBuiltin(lita, &VOID_TYPE)

    return &builtins
}

func AddBuiltin(lita: *Lita, type: *TypeInfo) {
    var sym = builtins.symbols.addSymbol(type.name,
                                        &builtins,
                                        NewBuiltinDecl(SrcPos{}, SrcPos{}, type, lita.allocator),
                                        SymbolFlags.IS_BUILTIN|SymbolFlags.IS_TYPE|SymbolFlags.IS_FOREIGN)

    sym.state = SymbolState.RESOLVED
    sym.type = type
    type.sym = sym
}

public func NewModule(lita: *Lita,
                      filename: *const char) : *Module {

    var mod = new<Module>(lita.allocator)
    mod.init(&lita.result, &lita.genericSymbols, lita.allocator)
    mod.id.fromFile(lita, filename)
    return mod
}

public func (this: *Module) init(result: *PhaseResult,
                                 genericSymbols: *Map<*const char, *Symbol>,
                                 allocator: *const Allocator) {
    this.text = null
    this.ast = null
    this.importedBy.init(64, allocator)
    this.symbols.init(ScopeKind.MODULE, &builtins.symbols, result, allocator)
    this.flags = 0
    this.allocator = allocator
    this.currentScope = &this.symbols
    this.genericSymbols = genericSymbols

    this.symbols.module = this // TEMP
}

public func (this: *Module) getType(type: *const char) : *Symbol {
    var sym = this.symbols.lookup(type)
    if(sym) {
        return sym
    }

    return this.genericSymbols.get(type)
}

public func (this: *Module) getFuncType(type: *const char) : *Symbol {
    var sym = this.symbols.lookupFunc(type)
    if(sym) {
        return sym
    }

    return this.genericSymbols.get(type) // TODO: Separate out Func in generic symbols
}

public const MAX_METHODS_FOR_TYPE = 2048; // TODO: this should be big enough??

func (this: *Module) isMethodForType(type: *TypeInfo, sym: *Symbol) : bool {
    if(!(sym.flags & SymbolFlags.IS_METHOD)) {
        return false
    }

    var fn = sym.type as (*FuncTypeInfo)
    if(fn.decl.params.size() == 0) {
        return false
    }

    var firstArg = fn.decl.params.get(0)
    if(!firstArg.typeInfo) {
        return false
    }

    var base = firstArg.typeInfo.getBaseType()
    if(IsGenericCapable(type)) {
        var gen = type as (*GenericTypeInfo)

        if(IsGenericCapable(base)) {
            var param = base as (*GenericTypeInfo)
            if(gen.genericTypeid == param.genericTypeid) {
                return true
            }
        }

        if(gen.genericTypeid == base.typeid) {
            return true
        }
    }

    if(base.typeid != type.typeid) {
        return false
    }

    return true
}

public func (this: *Module) getMethodsForType(type: *TypeInfo, result: [MAX_METHODS_FOR_TYPE]*Symbol) : i32 {
    var numOfSymbols = 0

    var currentSyms: *Scope = &this.symbols
    while(currentSyms) {
        for(var it = currentSyms.symbolFuncs.iter(); it.hasNext();) {
            var n = it.next()
            var sym = n.value

            if(!this.isMethodForType(type, sym)) {
                continue
            }

            result[numOfSymbols] = sym
            numOfSymbols += 1
        }

        currentSyms = currentSyms.parent ? currentSyms.parent : null
    }

/* I don't think this is needed?
    for(var it = this.genericSymbols.iter(); it.hasNext();) {
        var entry = it.next()
        var sym = entry.value

        if(!this.isMethodForType(type, sym)) {
            continue
        }

        result[numOfSymbols] = sym
        numOfSymbols += 1
    }
*/
    return numOfSymbols
}

public func (this: *Module) importModule(importDecl: *ImportDecl, moduleToImport: *Module) {
    var moduleName: *StringView = null;

    if(importDecl.alias.type == TokenType.IDENTIFIER) {
        moduleName = &importDecl.alias.str;
    }

    this.importModuleSymbols(moduleToImport, moduleName, importDecl.isUsing)
}


func (this: *Module) importModuleSymbols(moduleToImport: *Module, moduleName: *StringView, isUsing: bool) {
    moduleToImport.importedBy.add(ModuleImport{this, moduleName, isUsing})

    var symbols = moduleToImport.symbols;

    for(var i = 0; i < symbols.symbolTypes.capacity; i += 1) {
        var symName = symbols.symbolTypes.keys[i]
        var symbol = symbols.symbolTypes.values[i]
        if(!symName || !symbol) {
            continue
        }

        this.importModuleSymbol(moduleToImport, moduleName, symbol, symName, isUsing)
    }

    for(var i = 0; i < symbols.symbolFuncs.capacity; i += 1) {
        var symName = symbols.symbolFuncs.keys[i]
        var symbol = symbols.symbolFuncs.values[i]
        if(!symName || !symbol) {
            continue
        }

        this.importModuleSymbol(moduleToImport, moduleName, symbol, symName, isUsing)
    }
}

public func (this: *Module) importModuleSymbol(moduleToImport: *Module,
                                               moduleName: *StringView,
                                               symbol: *Symbol,
                                               symName: *const char,
                                               isUsing: bool) {

    // only import public symbols
    if(!(symbol.flags & SymbolFlags.IS_PUBLIC)) {
        return;
    }

    // only import symbols that are declared in the import module
    if(!symbol.declared.equals(moduleToImport)) {
        if(!symbol.usingModule || !symbol.usingModule.equals(moduleToImport)) {
            return;
        }
    }

    // don't import symbols already defined in this module
    if(symbol.declared.equals(this)) {
        return;
    }

    if(isUsing && !symbol.usingModule) {
        symbol.usingModule = this
    }

    if(moduleName) {
        var name = StringBufferInit(MAX_SYMBOL_NAME, this.allocator)
        name.appendStrn(moduleName.buffer, moduleName.length)
        name.appendStrn("::", 2)
        name.appendStr(symbol.name)

        this.symbols.importSymbol(name.cStrConst(), symbol)
    }
    else {
        this.symbols.importSymbol(symName, symbol)
    }
}

@inline
public func (this: *Module) equals(other: *Module) : bool {
    return this == other
    /*
    if(!other) return false
    if(this == other) return true
    return strcmp(this.id.filename, other.id.filename) == 0
    */
}

public func (this: *Module) print(header: *const char) {
    printf("Module %s %.*s {\n", header, this.id.name.length, this.id.name.buffer)
    var symbols = this.symbols

    for(var i = 0; i < symbols.symbolTypes.capacity; i+=1) {
        var symName = symbols.symbolTypes.keys[i]
        var symbol = symbols.symbolTypes.values[i]
        if(!symName || !symbol) {
            printf("   %d null\n", i)
            continue
        }
        printf("   %d '%s' %s\n", i, symName, SymbolKindAsStr(symbol.kind))
    }


    for(var i = 0; i < symbols.symbolFuncs.capacity; i+=1) {
        var symName = symbols.symbolFuncs.keys[i]
        var symbol = symbols.symbolFuncs.values[i]
        if(!symName || !symbol) {
            printf("   %d null\n", i)
            continue
        }
        printf("   %d '%s' %s\n", i, symName, SymbolKindAsStr(symbol.kind))
    }
    printf("}\n")
}

public func (moduleId: *ModuleId) fromFile(lita: *Lita, filename: *const char) {
    GetAbsolutePath(CurrentWorkingPath(), filename, moduleId.filename)
    var pathStr = StringViewInit(moduleId.filename);

    moduleId.packageName = StringViewInit(moduleId.filename, 0)
    moduleId.name = GetModuleName(pathStr)
}

public func GetModuleName(pathStr: StringView) : StringView {
    var endIndex = -1
    var startIndex = -1
    for(var i = pathStr.length - 1; i >= 0; i -= 1) {
        var c = pathStr.buffer[i]
        if(c == '.' && endIndex < 0) {
            endIndex = i
        }
        else if(c == '/') {
            startIndex = i + 1
            break
        }
    }

    if(endIndex < 0 || startIndex < 0 || startIndex > endIndex) {
        return StringView {
            .buffer = 0,
            .length = 0
        }
    }

    return StringView {
        .buffer = pathStr.buffer + startIndex,
        .length = endIndex - startIndex
    }
}

