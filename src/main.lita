import "std/libc"
import "std/mem"
import "std/mem/bucket_allocator"
import "std/mem/linear_allocator"
import "std/io"
import "std/cmdline"
import "std/system"
import "std/array"
import "std/map"
import "std/hash"
import "std/string/buffer"
import "std/string"
import "std/string/builder"
import "std/profile"
import "std/http"

import "lex"
import "lita"
import "types"
import "ast"
import "ast_new"
import "ast_print"
import "parser"
import "phase_result"
import "error_codes"
import "module"
import "checker"
import "cgen"
import "lsp"
import "intern"
import "build"
import "config"
import "pkg_mgr"


func main(len: i32, args: **char) : i32 {
    var options = new<LitaOptions>()
    memset(options, 0, sizeof(:LitaOptions))
    defer defaultAllocator.free(options)

    var status = ParseArgs(len, args, options)
    if(status != ParseStatus.OK) {
        return ParseStatus.TERMINATE == status ? 0 : -1;
    }

    if(options.pkgOptions.pkgCmd != PkgCommand.PKG_NONE) {
        return HandlePkgCommand(options)
    }

    return RunLitac(options)
}

public func RunLitac(options: *LitaOptions) : i32 {
    var startTime = SystemTimeMSec()

    var lita = Lita{}
    lita.init(options)
    defer lita.free()

    if(options.languageServer) {
        var server = LspServer{}
        server.init(&lita)
        defer server.free()

        server.start()

        return 0;
    }

    var root = lita.parse()
    if(!root) {
        goto report
    }

    if(!lita.typeCheck(root)) {
        goto report
    }

    if(options.checkerOnly) {
        goto report;
    }

    if(!lita.transpile(root)) {
        goto report;
    }

    var runProgram = options.run

report:
    var totalTime = SystemTimeMSec() - startTime

    var sb = StringBuilderInit(1024)
    defer sb.free()

    if(!lita.result.errors.empty()) {
        for(var i = 0; i < lita.result.errors.size(); i += 1) {
            PrintError(lita.options.isColoredOutput, sb, lita.result.errors.get(i))

            printf("%s\n", sb.cStr())
            sb.clear()
        }
        printf("Total errors: %d\n", lita.result.errors.size())
    }

    if(options.showProfileInfo) {
        var allocator: *BucketAllocator = lita.allocator as (*BucketAllocator)

        printf("\n\n")
        printf("%-30s %-17s %-6s %-14s %-5s\n", "Segment", "Time", "%", "Mem", "#")
        printf("====================================================================================\n")
        for(var i = 0; i < MetricType.MAX_METRIC_TYPES; i+=1) {
            var metric = &lita.metrics[i]
            printf("%-30s %-15.6f   %3.0f%% %-14zu %-5u\n",
                MetricTypeAsStr(i),
                metric.executionTime,
                (metric.executionTime / totalTime) * 100f,
                metric.bytesAllocated,
                metric.allocationCount)
        }
        printf("\n")
        printf("Total time:         %f\n", totalTime)
        printf("Total memory:       %zu\n", allocator.totalBytesAllocated)
        printf("Total allocations:  %u\n", allocator.totalAllocations)
        printf("Total source lines: %u\n", lita.totalSourceLines)
        printf("\n")
    }


    if(runProgram) {
        return lita.run()
    }

    return 0
}

public enum ParseStatus {
    OK,
    ERROR,
    TERMINATE,
}

public func ParseArgs(n: i32, args: **char, options: *LitaOptions) : ParseStatus {
    var parser = CmdParserInit()
    defer parser.free()

    parser.header = "<usage> litac [options] [source file to compile]\nOPTIONS:";
    parser.addOption("languageServer", '\0', "Start the LitaC language server", 0, null);
    parser.addOption("lib",            '\0', "The LitaC library path", OptionFlag.HAS_ARGUMENT, null);
    parser.addOption("cPrefx",         '\0', "The symbol prefix to use on the generated C code output", OptionFlag.HAS_ARGUMENT, null);
    parser.addOption("cFormat",        '\0', "Formats the C output", 0, null);
    parser.addOption("run",            '\0', "Runs the program after a successful compile", 0, null);
    parser.addOption("checkerOnly",    '\0', "Only runs the type checker, does not compile", 0, null);
    parser.addOption("cOnly",          '\0', "Only creates the C output file, does not cimple the generated C code", 0, null);
    parser.addOption("profile",        '\0', "Reports profile metrics of the compiler", 0, null);
    parser.addOption("disableLine",    '\0', "Disables #line dorective in C output", 0, null);
    parser.addOption("debug",          '\0', "Enables debug mode", 0, null);
    parser.addOption("verbose",        '\0', "Enables verbose output", 0, null);
    parser.addOption("colors",         '\0', "Enables console colors for output", 0, null);
    parser.addOption("instrument",     '\0', "Enables profile instrumentation of @profile annotated functions", 0, null);
    parser.addOption("srcDir",         '\0', "Specifies the source code directory, defaults to the parent folder of the supplied source file", OptionFlag.HAS_ARGUMENT, null);
    parser.addOption("doc",            '\0', "Generates document output", 0, null);
    parser.addOption("docDir",         '\0', "Directory where the generated documents are written to, defaults to './output'", OptionFlag.HAS_ARGUMENT, null);
    parser.addOption("docAll",         '\0', "Includes non-public types in the documentation generation", 0, null);
    parser.addOption("output",         'o', "The name of the compiled binary", OptionFlag.HAS_ARGUMENT, null);
    parser.addOption("outputDir",      '\0', "The directory in which the C ouput files are stored", OptionFlag.HAS_ARGUMENT, null);
    parser.addOption("version",        'v', "Displays the LitaC version", 0, null);
    parser.addOption("help",           'h', "Displays this help", 0, null);
    parser.addOption("types",          't', "Includes TypeInfo for reflection\n<arg> can be:\nall       Means all types will have reflection values\ntagged    Means only basic types and types annotated with @typeinfo will have reflection values", OptionFlag.HAS_ARGUMENT, null);
    parser.addOption("test",           '\0', "Runs functions annotated with @test\n<arg> is a regex of which tests should be run", OptionFlag.HAS_ARGUMENT, null);
    parser.addOption("testFile",       '\0', "Runs functions annotated with @test in the supplied source file only", 0, null);
    parser.addOption("testDir",        '\0', "Specifies the source code test directory", OptionFlag.HAS_ARGUMENT, null);
    parser.addOption("pkg-install",    '\0', "Scans for a pkg.json file and downloads and installs LitaC packages defined in the `dependencies` section.\nIf successful, creates a build.json which is used for building this LitaC project.", 0, null);
    parser.addOption("force",          '\0', "Forces a full clean install of all packages.  Must be ran with `pkg-install` option, otherwise will be ignored.", 0, null);
    parser.addOption("pkg-run",        '\0', "Runs a script defined in the `commands` section in this projects `pkg.json`.  The argument passed of this option is used to run the `commands.{option}` command", OptionFlag.HAS_ARGUMENT, null);
    parser.addOption("pkg-init",       '\0', "Initializes a new LitaC package project", OptionFlag.HAS_ARGUMENT, null);
    parser.addOption("pkg-build",      '\0', "Builds the LitaC package", 0, null);
    parser.addOption("release",        '\0', "Marks builds in release mode", 0, null);
    parser.addOption("proxy",          '\0', "Defines a proxy server to use when making network calls.  Ex. -proxy https://proxy.com:443", OptionFlag.HAS_ARGUMENT, null);
    parser.addOption("buildCmd",       'b',  "The underlying C compiler build and compile command.  Variables will be substituted if found:\n%output%\tThe executable name\n%input%\tThe file(s) generated.\n%options%\tThe generated @compiler_option's if -strict option is enabled. \nOtherwise, the LitaC compiler will append any @compiler_option flags to this command.", OptionFlag.HAS_ARGUMENT, null);
    parser.addOption("strict",         '\0', "The LitaC compiler will not modify the `buildCmd` option (for applying @compiler_option), instead it will store the generated @compiler_options in the %options% substitution variable.", 0, null);
    parser.addOption("tcc",            '\0', "Use tcc to compile.  This ignores buildCmd/strictBuildCmd", 0, null);
    parser.addOption("maxMemory",      '\0', "The max amount of memory allowed for allocation. NOTE: This is a temporary option", OptionFlag.HAS_ARGUMENT, null);
    parser.addOption("--",             '\0', "Signals any arguments after are to be passed to the compile program when -run is set", 0, null)

    if(parser.parse(n, args) != CmdParserStatus.OK) {
        goto err_print;
    }


    if(parser.hasOption("version")) {
        printf("Version: %s\n", LITA_VERSION)
        goto terminate;
    }

    if(parser.hasOption("colors")) {
        options.isColoredOutput = true
    }

    if(parser.hasOption("help")) {
        {
            var sb = StringBuilderInit()
            defer sb.free()

            parser.printHelp(sb)
            printf("%s\n", sb.cStr())
        }
        goto terminate;
    }

    options.pkgOptions.pkgCmd = PkgCommand.PKG_NONE
    if(parser.hasOption("pkg-install")) {
        options.pkgOptions.pkgCmd = PkgCommand.PKG_INSTALL
        options.pkgOptions.forceClean = parser.hasOption("force")
    }

    if(parser.hasOption("pkg-run")) {
        options.pkgOptions.pkgCmd = PkgCommand.PKG_RUN
        options.pkgOptions.pkgRunCmdArg = parser.getOption("pkg-run").value
    }

    if(parser.hasOption("pkg-init")) {
        options.pkgOptions.pkgCmd = PkgCommand.PKG_INIT
        options.pkgOptions.pkgName = parser.getOption("pkg-init").value
    }

    if(parser.hasOption("pkg-build")) {
        options.pkgOptions.pkgCmd = PkgCommand.PKG_BUILD
        options.pkgOptions.isRelease = parser.hasOption("release")
    }

    options.languageServer = parser.hasOption("languageServer")
    if(!options.languageServer) {
        var pkgCmd = options.pkgOptions.pkgCmd

        // if we are not running a pkg* command, we must have an input file
        if(pkgCmd == PkgCommand.PKG_NONE) {
            if(parser.args.size() < 2) {
                printf("Missing input file\n")
                goto err;
            }

            GetAbsolutePath(CurrentWorkingPath(), parser.args.get(1), options.inputFile)
        }
        else {
            // we optionally can provide an input file for pkg* commands
            var hasInputFile = (pkgCmd != PkgCommand.PKG_RUN || parser.hasOption("run"))
            if(parser.args.size() > 1 && hasInputFile) {
                GetAbsolutePath(CurrentWorkingPath(), parser.args.get(1), options.inputFile)
                if(!FileExists(options.inputFile)) {
                    printf("Could not find '%s'\n", options.inputFile)
                    goto err;
                }
            }
        }
    }

    var lib:*const char = GetEnv("LITAC_HOME")
    if(!lib) lib = LITAC_HOME_DEFAULT;

    GetAbsolutePath("", lib, options.litaPath)

    options.checkerOnly = parser.hasOption("checkerOnly")
    options.cOutputOnly = parser.hasOption("cOnly")
    options.showProfileInfo = parser.hasOption("profile")
    options.run = parser.hasOption("run")
    options.isDebug = parser.hasOption("debug")
    options.cFormat = parser.hasOption("cFormat")
    options.instrument = parser.hasOption("instrument")
    options.disableLineSync = parser.hasOption("disableLine")
    options.isVerbose = parser.hasOption("verbose")
    options.isTcc = parser.hasOption("tcc")
    options.isStrict = parser.hasOption("strict")

    if(parser.hasOption("maxMemory")) {
        var memoryStr = parser.getOption("maxMemory").value
        var view = StringInit(memoryStr)

        options.maxMemory = strtol(memoryStr, null, 10)
        if(view.indexOf($"GB") > -1 || view.indexOf($"GiB") > -1 ||
           view.indexOf($"gb") > -1 || view.indexOf($"gib") > -1) {
            options.maxMemory *= GiB
        }
        else if(view.indexOf($"MB") > -1 || view.indexOf($"MiB") > -1 ||
           view.indexOf($"mb") > -1 || view.indexOf($"mib") > -1) {
            options.maxMemory *= MiB
        }
        else if(view.indexOf($"KB") > -1 || view.indexOf($"KiB") > -1 ||
           view.indexOf($"kb") > -1 || view.indexOf($"kib") > -1) {
            options.maxMemory *= KiB
        }
    }
    else {
        options.maxMemory = 300 * MiB
    }

    options.typeOption = TypeInfoOption.NONE
    if(parser.hasOption("types")) {
        var opt = parser.getOption("types").value
        if(opt.equals("all")) {
            options.typeOption = TypeInfoOption.ALL
        }
        else if(opt.equals("tagged")) {
            options.typeOption = TypeInfoOption.TAGGED
        }
    }

    if(parser.hasOption("test")) {
        options.testsRegex = parser.getOption("test").value
        options.testsOnly = true
    }

    if(parser.hasOption("testFile")) {
        options.testFileOnly = true
        options.testsOnly = true
    }

    if(parser.hasOption("proxy")) {
        options.proxy = parser.getOption("proxy").value
    }

    if(parser.hasOption("buildCmd")) {
        options.compileCmd = parser.getOption("buildCmd").value
    }
    else {
        options.compileCmd = BUILD_CMD_DEFAULT;
    }

    var cPrefix = parser.hasOption("cPrefix") ? parser.getOption("cPrefix").value : "litaC_"
    StringCopy(.src = cPrefix, .dest = options.cPrefix, .size = MAX_PREFIX_SIZE)

    var outputFile = parser.hasOption("output") ? parser.getOption("output").value : OUTPUT_NAME_DEFAULT
    StringCopy(.src = outputFile, .dest = options.outputFile, .size = MAX_PATH)

    var outputPath = parser.hasOption("outputDir") ? parser.getOption("outputDir").value : OUTPUT_DIR_DEFAULT
    StringCopy(.src = outputPath, .dest = options.outputPath, .size = MAX_PATH)

    var len = options.outputPath.length()
    if(len > 0) {
        if(options.outputPath[len-1] != '/') {
            strcat(options.outputPath, "/")
        }

        var output = [MAX_PATH]char{0}
        GetAbsolutePath(CurrentWorkingPath(), options.outputPath, output)
        StringCopy(.src = output, .dest = options.outputPath, .size = MAX_PATH)
    }

    if(parser.hasOption("srcDir")) {
        StringCopy(.src = parser.getOption("srcDir").value, .dest = options.srcPath, .size = MAX_PATH)
    }

    if(options.srcPath.empty()) {
        var path = StringInit(options.inputFile, MAX_PATH)
        var index = path.lastIndexOfAt($"/src/")
        if(index > -1) {
            StringCopy(.src = options.inputFile, .dest = options.srcPath, .size = index + 5)
        }
        else {
            FileParent(options.inputFile, options.srcPath)
        }
    }

    if(parser.hasOption("testDir")) {
        StringCopy(.src = parser.getOption("testDir").value, .dest = options.testPath, .size = MAX_PATH)
    }
    else {
        StringCopy(.src = "/test", .dest = options.testPath, .size = MAX_PATH)
    }

    // if there is no source path
    if(options.srcPath.empty()) {
        StringCopy(.src = CurrentWorkingPath(), .dest = options.projectPath, .size = MAX_PATH)
    }
    else {
        FileParent(options.srcPath, options.projectPath)
    }

    if(options.projectPath.empty()) {
        StringCopy(.src = ".", .dest = options.projectPath, .size = 2)
    }

    if(parser.hasOption("lib")) {
        StringCopy(.src = parser.getOption("lib").value, .dest = options.libPath, .size = MAX_PATH)
    }

    // sanitize paths
    TrimPath(options.projectPath)
    TrimPath(options.srcPath)
    TrimPath(options.libPath)
    TrimPath(options.litaPath)
    TrimPath(options.testPath)

    // determine if we need to collect arguments
    if(options.run || options.pkgOptions.pkgCmd == PkgCommand.PKG_RUN) {
        for(var i = 0; i < n; i+=1) {
            var arg = args[i]
            if("--".equals(arg)) {
                i += 1
                options.args = (args + i)
                options.nargs = n - i
                break;
            }
        }
    }

    return ParseStatus.OK

terminate:
    return ParseStatus.TERMINATE

err_print:
    {
        var sb = StringBuilderInit()
        defer sb.free()

        parser.printHelp(sb)
        printf("%s", sb.cStr())
    }
err:
    return ParseStatus.ERROR
}

func TrimPath(path: [MAX_PATH]char) {
    var len = path.length()
    if(len < 1) {
        return;
    }

    if(path[len - 1] == '/' || path[len - 1] == '\\') {
        path[len - 1] = '\0'
    }
}

func HandlePkgCommand(options: *LitaOptions) : i32 {
    var size: usize = options.maxMemory > 0
        ? options.maxMemory
        : 1024 * 1024 * 1024;

    var linearAllocator = LinearAllocator{}
    linearAllocator.init(defaultAllocator.alloc(size), size)
    defer defaultAllocator.free(linearAllocator.mem)

    var pm = PackageManager{};
    pm.init(PackageOptions{
        .projectPath = options.projectPath,
        .litaOptions = options,

    }, linearAllocator.allocator)
    defer pm.free()

    switch(options.pkgOptions.pkgCmd) {
        case PkgCommand.PKG_INSTALL: {
            return RunPkgInstall(options, &pm)
        }
        case PkgCommand.PKG_RUN: {
            return RunPkgCommand(options, &pm)
        }
        case PkgCommand.PKG_INIT: {
            return RunPkgInit(options, &pm)
        }
        case PkgCommand.PKG_BUILD: {
            return RunPkgBuild(options, &pm)
        }
        default:
            return 0;
    }

    return 0;
}

func RunPkgInstall(options: *LitaOptions, pm: *PackageManager) : i32 {
    var status = pm.installCommand(PackageInstallOptions {
        .fullSync = options.pkgOptions.forceClean,
        .httpOptions = HttpOptions {
            .proxy = options.proxy,
        }
    })
    if(status != PkgStatus.OK) {
        printf("Error installing packages:\nErrorCode: %s - %s\n",
            PkgStatusAsStr(status), pm.errors.cStr())
        return 1;
    }

    var buildFile = BuildFile{}
    defer buildFile.free()

    status = pm.createBuildFile(&buildFile)
    if(status != PkgStatus.OK) {
        printf("Error creating build.json:\nErrorCode: %s - %s\n",
            PkgStatusAsStr(status), pm.errors.cStr())
        return 1;
    }

    var temp: [MAX_PATH]char;
    var build = StringBufferInit(temp, MAX_PATH, 0)
    build.format("%s/.build.json", options.projectPath)

    var file = File{}
    file.open(build.cStr(), FileOpenOp.WRITE)
    defer file.close()

    printf("Writing '%s'.\n", build.cStr())
    if(!buildFile.writeTo(&file)) {
        printf("Error writing out build.json\n")
        return 1;
    }

    printf("Success.\n")
    return 0;
}

func RunPkgCommand(options: *LitaOptions, pm: *PackageManager) : i32 {
    var status = pm.runCommand(options.pkgOptions.pkgRunCmdArg)
    if(status != PkgStatus.OK) {
        printf("Error running pkg command: '%s'\nErrorCode: %s %s\n",
            options.pkgOptions.pkgRunCmdArg,
            PkgStatusAsStr(status),
            pm.errors.cStr()
        )
        return 1;
    }
    return 0;
}


func RunPkgInit(options: *LitaOptions, pm: *PackageManager) : i32 {
    var status = pm.initCommand(PackageInitOptions {
        .name = options.pkgOptions.pkgName
    })
    if(status != PkgStatus.OK) {
        printf("Error running pkg init:\nErrorCode: %s - %s\n",
            PkgStatusAsStr(status), pm.errors.cStr())
        return 1;
    }
    return 0;
}

func RunPkgBuild(options: *LitaOptions, pm: *PackageManager) : i32 {
    var status = pm.buildCommand(PackageBuildOptions {
        .isRelease = options.pkgOptions.isRelease
    })
    if(status != PkgStatus.OK) {
        printf("Error running pkg build:\nErrorCode: %s - %s\n",
            PkgStatusAsStr(status), pm.errors.cStr())
        return 1;
    }
    return 0;
}
