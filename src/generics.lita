import "std/string"
import "std/libc"
import "std/assert"
import "std/mem"
import "std/map"
import "std/array"
import "std/string_view"

import "symbols"
import "ast"
import "ast_copy"
import "types"
import "lita"
import "module"
import "checker"
import "common"
import "ast_print"
import "intern"

public const MAX_GENERIC_NAME = 1024;

func GetGenericName(checker: *TypeChecker, typeSpec: *TypeSpec, str: *String, fullyQualified: bool) {
    if(typeSpec == null) {
        return;
    }

    switch(typeSpec.kind) {
        case TypeSpecKind.ARRAY: {
            str.append("[]")
            GetGenericName(checker, typeSpec.base, str, fullyQualified)
            return;
        }
        case TypeSpecKind.PTR: {
            str.append("*")
            GetGenericName(checker, typeSpec.base, str, fullyQualified)
            return;
        }
        case TypeSpecKind.CONST: {
            str.append("const ")
            GetGenericName(checker, typeSpec.base, str, fullyQualified)
            return;
        }
        case TypeSpecKind.NAME: {
            var name = typeSpec

            // We must account for typedef'd types, if they
            // are typedef'd we need to use the real type
            // for the naming
            var nameSet = false
            var sym: *Symbol = null
            var typeInfo: *TypeInfo = null
            var isAlias = false

            // if this typespec hasn't been resolved,
            // get the associated type
            if(!typeSpec.typeInfo) {
                sym = checker.getType(name)
                if(sym) {
                    typeInfo = sym.type

                    // get the real type, not the typedef
                    if(sym.isAliased()) {
                        sym = sym.type.sym
                        isAlias = true
                    }

                    if(typeInfo && !typeInfo.name.empty()) {
                        if(!IsPrimitive(typeInfo) && fullyQualified && sym) {
                            var name = sym.declared.id.packageName
                            str.append("%.*s::", name.length, name.buffer)
                        }

                        var baseName = typeInfo.getBaseNameView()
                        str.appendStrn(baseName.buffer, baseName.length)
                        nameSet = true
                    }
                }
            }
            else {
                typeInfo = typeSpec.typeInfo
                sym = typeInfo.sym

                // get the real type, not the typedef
                if(sym.isAliased()) {
                    sym = sym.type.sym
                    isAlias = true
                }
            }

            var useSymbolName = true

            // ensure generic types contain generic arguments
            if(sym && !sym.genericArgs.empty() && !isAlias) {
                var newName = sym.type.asTypeSpec(checker.current, true)

                name = newName
                useSymbolName = false
            }

            if(sym && !nameSet && useSymbolName) {
                var isGeneric = (sym.flags & (SymbolFlags.IS_FROM_GENERIC_TEMPLATE|SymbolFlags.IS_GENERIC_TEMPLATE));
                var isBuiltin = (sym.flags & SymbolFlags.IS_BUILTIN)
                var includePackage = !isGeneric && !isBuiltin && fullyQualified && !IsPrimitive(sym.type);

                // include the fully qualified package name if not already included
                if(includePackage) {
                    var name = sym.declared.id.packageName
                    str.append("%.*s::", name.length, name.buffer)
                }

                // if this isn't a generic type, we want to use the symbol name,
                // otherwise the TypeSpec name should be used
                if(!isGeneric && useSymbolName) {
                    str.append("%.*s", sym.name.length, sym.name.buffer)
                    nameSet = true;
                }
            }

            if(!nameSet) {
                str.appendStrn(name.name.buffer, name.name.length)
            }

            if(name.genericArgs.empty()) {
                return;
            }

            str.append("<")
            for(var i = 0; i < name.genericArgs.size(); i+=1) {
                if(i > 0) {
                    str.append(",")
                }
                var arg = name.genericArgs.get(i)
                GetGenericName(checker, arg, str, fullyQualified)
            }
            str.append(">")
            return;
        }
        case TypeSpecKind.FUNC_PTR: {
            str.append("func(")
            for(var i = 0; i < typeSpec.args.size(); i+=1) {
                if(i > 0) {
                    str.append(",")
                }
                var arg = typeSpec.args.get(i)
                GetGenericName(checker, arg, str, fullyQualified)
            }
            str.append("):")
            if(typeSpec.ret) {
                GetGenericName(checker, typeSpec.ret, str, fullyQualified)
            }
            else {
                str.append("void")
            }
            break;
        }
        case TypeSpecKind.NONE:
        default:
    }

}

public func CreateTypeFromGenericTemplate(checker: *TypeChecker,
                                          sym: *Symbol,
                                          name: *TypeSpec) : *Symbol {
    assert(sym != null)

    // Setup the generic context to support proper type look up
    // for generic types recursively creating other generic types -
    // which need the appropriate callsite module to properly resolve
    // the types
    var isNewContext = checker.genericContext.callsite == null
    if(isNewContext) {
        checker.genericContext.callsite = checker.current
    }
    defer if(isNewContext) checker.genericContext.callsite = null

    var buffer: [MAX_GENERIC_NAME]char;
    var genericName = StringInit(buffer, MAX_GENERIC_NAME, 0)

    // We must make fully qualifed generic names to avoid conflicts/duplicates
    GetGenericName(checker, name, genericName, true)
    var lookupNameStr = checker.lita.strings.internCopy(genericName.cStrConst(), genericName.length)

    var genericSym = checker.current.genericSymbols.get(lookupNameStr)
    const module = checker.current


    // if this is recompilation, we want to force creating a new
    // Declaration type
    if(genericSym && genericSym.flags & SymbolFlags.IS_MARKED_RESET) {
        genericSym = null
    }

    if(!genericSym) {

        // If the symbol is an aliased type, we must ensure
        // we copy the declaration of the real type
        if(sym.flags & SymbolFlags.IS_ALIAS) {
            var unaliasedSym = checker.getTypeByName(sym.type.name)
            if(unaliasedSym) {
                sym = unaliasedSym
            }
        }

        var genericDecl = CreateDecl(sym, name.genericArgs, module)
        if(!genericDecl) {
            goto err;
        }


        genericName.clear()
        GetGenericName(checker, name, genericName, false)
        var newNameStr = checker.lita.strings.internCopy(genericName.cStrConst(), genericName.length)

        genericSym = checker.current.symbols.addSymbol(newNameStr,
                                                       sym.declared,
                                                       genericDecl,
                                                       SymbolFlags.IS_INCOMPLETE|
                                                       SymbolFlags.IS_FROM_GENERIC_TEMPLATE)

        if(!genericSym) {
            goto err;
        }

        genericSym.genericCallsiteModule = checker.current
        genericSym.genericParams = genericDecl.genericParams.copy(checker.allocator)

        checker.current.genericSymbols.put(lookupNameStr, genericSym)
        if(genericSym.decl.kind == StmtKind.FUNC_DECL) {
            checker.symbolFuncs.add(genericSym)
        }
        else {
            checker.symbolTypes.add(genericSym)
        }

        // if this still has generic parameters, add in the original
        // parameters
        if(HasGenericParam(checker, name.genericArgs)) {
            genericSym.flags |= SymbolFlags.IS_GENERIC_TEMPLATE

            var originalDecl = sym.decl as (*GenericDecl)

            genericDecl.genericParams = originalDecl.genericParams.copy(checker.allocator)
        }
        else {
            // Remove any generic parameters from the declaration that
            // have been defined a type
            var removed = 0
            for(var i = 0; i < name.genericArgs.size(); i+=1) {
                var index = i - removed
                if(index < genericDecl.genericParams.size()) {
                    genericDecl.genericParams.removeAt(index)
                    removed+=1;
                }
            }
        }

        var numOfGenericArgs = name.genericArgs.size();
        genericSym.genericArgs.init(numOfGenericArgs, checker.allocator)

        var isValidArgs = true
        for(var i = 0; i < numOfGenericArgs; i+=1) {
            var arg = name.genericArgs.get(i)
            var typeInfo = checker.resolveTypeSpec(arg)
            if(!typeInfo || typeInfo.kind == TypeKind.POISON) {
                isValidArgs = false
            }

            genericSym.genericArgs.add(arg.typeInfo)
        }

        if(!isValidArgs) {
            goto err;
        }
    }
    else {
        // if this symbol is already resolved, we still need resolve the
        // typespec arguments
        var isValidArgs = true
        for(var i = 0; i < name.genericArgs.size(); i+=1) {
            var arg = name.genericArgs.get(i)

            var typeInfo = checker.resolveTypeSpec(arg)
            if(!typeInfo || typeInfo.kind == TypeKind.POISON) {
                isValidArgs = false
            }
        }

        if(!isValidArgs) {
            goto err;
        }
    }

    assert(genericSym != null)

    checker.resolveSymbol(genericSym)
    checker.finishResolveSymbol(genericSym)

    name.typeInfo = genericSym.type
    if(IsGenericCapable(genericSym.type) && sym.type) {
        var genericType = genericSym.type
        genericType.genericTypeid = sym.type.typeid
    }

    return genericSym

err:
    return null
}

func CreateDecl(sym: *Symbol,
                genericArgs: *Array<*TypeSpec>,
                module: *Module) : *GenericDecl {

    var genericDecl: *GenericDecl = null;

    switch(sym.decl.kind) {
        case StmtKind.FUNC_DECL: {
            genericDecl = CreateFuncDecl(sym.decl as (*FuncDecl), genericArgs, module)
            break;
        }
        case StmtKind.TRAIT_DECL:
        case StmtKind.UNION_DECL:
        case StmtKind.STRUCT_DECL: {
            genericDecl = CreateAggregateDecl(sym.decl as (*AggregateDecl), genericArgs, module)
            break;
        }
        case StmtKind.TYPEDEF_DECL: {
            genericDecl = CreateTypedefDecl(sym.decl as (*TypedefDecl), genericArgs, module)
            break;
        }
        default: {
            assert(false)
        }
    }

    return genericDecl
}

func CreateAggregateDecl(aggDecl: *AggregateDecl,
                         genericArgs: *Array<*TypeSpec>,
                         module: *Module) : *GenericDecl {
    var copy = CopyAggregateDecl(aggDecl, module)

    var template = Template {
        .genericParams = &aggDecl.genericParams,
        .genericArgs = genericArgs
    }

    ReplaceTypes(&template, copy.node)

    return copy as (*GenericDecl)
}


func CreateFuncDecl(funcDecl: *FuncDecl,
                    genericArgs: *Array<*TypeSpec>,
                    module: *Module) : *GenericDecl {

    var copy = CopyFuncDecl(funcDecl, module)

    var template = Template {
        .genericParams = &funcDecl.genericParams,
        .genericArgs = genericArgs
    }

    ReplaceTypes(&template, copy.node)

    return copy as (*GenericDecl)
}

func CreateTypedefDecl(typedefDecl: *TypedefDecl,
                       genericArgs: *Array<*TypeSpec>,
                       module: *Module) : *GenericDecl {
    var copy = CopyTypedefDecl(typedefDecl, module)

    var template = Template {
        .genericParams = &typedefDecl.genericParams,
        .genericArgs = genericArgs
    }

    ReplaceTypes(&template, copy.node)

    return copy as (*GenericDecl)
}


struct Template {
    genericParams: *Array<GenericParam>
    genericArgs: *Array<*TypeSpec>
}

func (this: *Template) replaceTypeSpec(type: *TypeSpec) : *TypeSpec {
    if(!type) {
        return null
    }

    switch(type.kind) {
        case TypeSpecKind.NONE: {
            return null
        }
        case TypeSpecKind.ARRAY: {
            type.base = this.replaceTypeSpec(type.base)
            return type
        }
        case TypeSpecKind.CONST: {
            type.base = this.replaceTypeSpec(type.base)
            return type
        }
        case TypeSpecKind.PTR: {
            type.base = this.replaceTypeSpec(type.base)
            return type
        }
        case TypeSpecKind.FUNC_PTR: {
            var funcPtr = type

            for(var i = 0; i < funcPtr.args.size(); i+=1) {
                funcPtr.args.set(i, this.replaceTypeSpec(funcPtr.args.get(i)))
            }

            funcPtr.ret = this.replaceTypeSpec(funcPtr.ret)

            for(var i = 0; i < funcPtr.genericParams.size();) {
                var isRemoved = false
                var genParam = funcPtr.genericParams.get(i)
                for(var j = 0; j < this.genericParams.size(); j+=1) {
                    var param = this.genericParams.get(j)

                    if(param.name.str.equals(genParam.name.str)) {
                        funcPtr.genericParams.removeAt(i)
                        isRemoved = true
                        break;
                    }
                }

                if(!isRemoved) {
                    i+=1
                }
            }

            return type
        }
        case TypeSpecKind.NAME: {
            var name = type

            for(var i = 0; i < name.genericArgs.size(); i+=1) {
                name.genericArgs.set(i, this.replaceTypeSpec(name.genericArgs.get(i)))
            }

            for(var i = 0; i < this.genericParams.size(); i+=1) {
                var param = this.genericParams.get(i)
                if(param.name.str.equals(name.name)) {
                    if(i < this.genericArgs.size()) {
                        type = this.genericArgs.get(i)
                        break;
                    }
                }
            }

            return type
        }
        default: {
            assert(false)
        }
    }

    return null
}


func ReplaceTypes(template: *Template, ast: *Node) : bool {
    if(!ast) return true;

    switch(ast.kind) {
        case StmtKind.TRAIT_DECL:
        case StmtKind.UNION_DECL:
        case StmtKind.STRUCT_DECL: {
            var aggDecl = ast as (*AggregateDecl)
            for(var i = 0; i < aggDecl.fields.size(); i+=1) {
                var field = aggDecl.fields.get(i)
                switch(field.kind) {
                    case StmtKind.TRAIT_FIELD_DECL: {
                        var traitField = field.traitField
                        traitField.type = template.replaceTypeSpec(traitField.type)
                        break;
                    }
                    case StmtKind.VAR_FIELD_DECL: {
                        var varField = field.varField
                        varField.type = template.replaceTypeSpec(varField.type)
                        break;
                    }
                    case StmtKind.ENUM_FIELD_DECL: {
                        break;
                    }
                    case StmtKind.UNION_FIELD_DECL:
                    case StmtKind.STRUCT_FIELD_DECL: {
                        var aggField = field.aggregateField
                        ReplaceTypes(template, aggField.node)
                        break;
                    }
                    case StmtKind.POISON_EXPR: {
                        break;
                    }
                    default: {
                        assert(false)
                    }
                }
            }
            break;
        }
        case StmtKind.FUNC_DECL: {
            var funcDecl = ast as (*FuncDecl)
            for(var i = 0; i < funcDecl.params.size(); i+=1) {
                var param = funcDecl.params.get(i)

                param.type = template.replaceTypeSpec(param.type)

                if(param.defaultExpr) {
                    ReplaceTypes(template, param.defaultExpr)
                }
            }

            funcDecl.returnType = template.replaceTypeSpec(funcDecl.returnType)
            ReplaceTypes(template, funcDecl.body)
            break;
        }
        case StmtKind.IMPORT_DECL: {
            break;
        }
        case StmtKind.CONST_DECL:
        case StmtKind.VAR_DECL: {
            var decl = ast as (*VarDecl)
            decl.typeSpec = template.replaceTypeSpec(decl.typeSpec)

            if(decl.expr) {
                ReplaceTypes(template, decl.expr)
            }
            break;
        }
        case StmtKind.TYPEDEF_DECL: {
            var decl = ast as (*TypedefDecl)
            decl.type = template.replaceTypeSpec(decl.type)
            break;
        }
        case StmtKind.ENUM_DECL:
        case StmtKind.PARAM_DECL:
        case StmtKind.NATIVE_DECL:
        case StmtKind.POISON_DECL: {
            break;
        }



        case StmtKind.BLOCK_STMT: {
            var body = ast as (*BlockStmt)
            for(var i = 0; i < body.stmts.size(); i+=1) {
                var s = body.stmts.get(i)
                ReplaceTypes(template, s)
            }
            break;
        }
        case StmtKind.BREAK_STMT: {
            break;
        }
        case StmtKind.COMP_STMT: {
            var comp = ast as (*CompStmt)
            if(comp.evaluatedStmt) {
                ReplaceTypes(template, comp.evaluatedStmt)
            }
            if(comp.end) {
                ReplaceTypes(template, comp.end)
            }
            for(var i = 0; i < comp.body.size(); i+=1) {
                var s = comp.body.get(i)
                ReplaceTypes(template, s)
            }
            break;
        }
        case StmtKind.CONTINUE_STMT: {
            break;
        }
        case StmtKind.DEFER_STMT: {
            var deferStmt = ast as (*DeferStmt)
            ReplaceTypes(template, deferStmt.deferedStmt)
            break;
        }
        case StmtKind.DO_WHILE_STMT: {
            var whileStmt = ast as (*DoWhileStmt)
            ReplaceTypes(template, whileStmt.body)
            ReplaceTypes(template, whileStmt.cond)
            break;
        }
        case StmtKind.EMPTY_STMT: {
            return true;
        }
        case StmtKind.FOR_STMT: {
            var forStmt = ast as (*ForStmt)
            if(forStmt.init) {
                ReplaceTypes(template, forStmt.init)
            }
            if(forStmt.cond) {
                ReplaceTypes(template, forStmt.cond)
            }
            if(forStmt.post) {
                ReplaceTypes(template, forStmt.post)
            }
            if(forStmt.body) {
                ReplaceTypes(template, forStmt.body)
            }
            break;
        }
        case StmtKind.FUNC_BODY_STMT: {
            var body = ast as (*FuncBodyStmt)

            for(var i = 0; i < body.stmts.size(); i+=1) {
                var s = body.stmts.get(i)
                ReplaceTypes(template, s)
            }
            break;
        }
        case StmtKind.GOTO_STMT: {
            break;
        }
        case StmtKind.IF_STMT: {
            var ifStmt = ast as (*IfStmt)
            ReplaceTypes(template, ifStmt.cond)
            ReplaceTypes(template, ifStmt.then)
            if(ifStmt.elseStmt) {
                ReplaceTypes(template, ifStmt.elseStmt)
            }
            break;
        }
        case StmtKind.LABEL_STMT: {
            break;
        }
        case StmtKind.MODULE_STMT: {
            break;
        }
        case StmtKind.NOTE_STMT: {
            break;
        }
        case StmtKind.PARAMETERS_STMT: {
            var params = ast as (*ParametersStmt)
            for(var i = 0; i < params.size(); i += 1) {
                var param = params.get(i)
                ReplaceTypes(template, param as (*Node))
            }
            break;
        }
        case StmtKind.RETURN_STMT: {
            var stmt = ast as (*ReturnStmt)
            if(stmt.expr) {
                ReplaceTypes(template, stmt.expr)
            }
            break;
        }
        case StmtKind.SWITCH_CASE_STMT: {
            var caseStmt = ast as (*SwitchCaseStmt)
            ReplaceTypes(template, caseStmt.cond)
            ReplaceTypes(template, caseStmt.body)
            break;
        }
        case StmtKind.SWITCH_STMT:{
            var switchStmt = ast as (*SwitchStmt)
            ReplaceTypes(template, switchStmt.cond)
            for(var i = 0; i < switchStmt.cases.size(); i+=1) {
                var caseStmt = switchStmt.cases.get(i)
                ReplaceTypes(template, caseStmt as (*Node))
            }
            if(switchStmt.defaultStmt) {
                ReplaceTypes(template, switchStmt.defaultStmt)
            }
            break;
        }
        case StmtKind.ENUM_FIELD_DECL:
        case StmtKind.ENUM_FIELD_ENTRY_DECL:
        case StmtKind.STRUCT_FIELD_DECL:
        case StmtKind.UNION_FIELD_DECL:
        case StmtKind.TRAIT_FIELD_DECL:
        case StmtKind.VAR_FIELD_DECL: {
            // TODO: Is this needed??
            break;
        }
        case StmtKind.WHILE_STMT: {
            var whileStmt = ast as (*WhileStmt)
            ReplaceTypes(template, whileStmt.cond)
            ReplaceTypes(template, whileStmt.body)
            break;
        }


        /*--------------------------------------
            Expressions
        ----------------------------------------*/
        case StmtKind.ARRAY_DESIGNATION_EXPR: {
            var expr = ast as (*ArrayDesignationExpr)
            ReplaceTypes(template, expr.index)
            ReplaceTypes(template, expr.value)
            break;
        }
        case StmtKind.ARRAY_INIT_EXPR: {
            var expr = ast as (*ArrayInitExpr)
            expr.type = template.replaceTypeSpec(expr.type)
            for(var i = 0; i < expr.values.size(); i += 1) {
                ReplaceTypes(template, expr.values.get(i))
            }
            break;
        }
        case StmtKind.BINARY_EXPR: {
            var expr = ast as (*BinaryExpr)
            ReplaceTypes(template, expr.left)
            ReplaceTypes(template, expr.right)
            break;
        }
        case StmtKind.BOOLEAN_EXPR: {
            break;
        }
        case StmtKind.CAST_EXPR: {
            var expr = ast as (*CastExpr)
            ReplaceTypes(template, expr.exprToCast)
            expr.castTo = template.replaceTypeSpec(expr.castTo)
            break;
        }
        case StmtKind.CHAR_EXPR: {
            break;
        }
        case StmtKind.FUNC_CALL_EXPR: {
            var expr = ast as (*FuncCallExpr)
            ReplaceTypes(template, expr.object)
            for(var i = 0; i < expr.genericArgs.size(); i += 1) {
                var arg = expr.genericArgs.get(i)
                arg = template.replaceTypeSpec(arg)
                expr.genericArgs.set(i, arg)
            }

            for(var i = 0; i < expr.arguments.size(); i += 1) {
                ReplaceTypes(template, expr.arguments.get(i).argExpr)
            }
            break;
        }
        case StmtKind.FUNC_IDENTIFIER_EXPR: {
            var expr = ast as (*IdentifierExpr)
            expr.type = template.replaceTypeSpec(expr.type)
            break;
        }
        case StmtKind.GET_EXPR: {
            var expr = ast as (*GetExpr)
            ReplaceTypes(template, expr.object)
            ReplaceTypes(template, expr.field as (*Node))
            break;
        }
        case StmtKind.GROUP_EXPR: {
            var expr = ast as (*GroupExpr)
            ReplaceTypes(template, expr.groupedExpr)
            break;
        }
        case StmtKind.IDENTIFIER_EXPR: {
            var expr = ast as (*IdentifierExpr)
            expr.type = template.replaceTypeSpec(expr.type)
            break;
        }
        case StmtKind.INIT_EXPR: {
            var expr = ast as (*InitExpr)
            expr.type = template.replaceTypeSpec(expr.type)

            for(var i = 0; i < expr.genericArgs.size(); i += 1) {
                var arg = expr.genericArgs.get(i)
                arg = template.replaceTypeSpec(arg)
                expr.genericArgs.set(i, arg)
            }

            for(var i = 0; i < expr.arguments.size(); i += 1) {
                ReplaceTypes(template, expr.arguments.get(i) as (*Node))
            }
            break;
        }
        case StmtKind.INIT_ARG_EXPR: {
            var expr = ast as (*InitArgExpr)
            ReplaceTypes(template, expr.value)
            break;
        }
        case StmtKind.NULL_EXPR: {
            break;
        }
        case StmtKind.NUMBER_EXPR: {
            break;
        }
        case StmtKind.OFFSET_OF_EXPR: {
            var expr = ast as (*OffsetOfExpr)
            expr.type = template.replaceTypeSpec(expr.type)
            break;
        }
        case StmtKind.SET_EXPR: {
            var expr = ast as (*SetExpr)
            ReplaceTypes(template, expr.object)
            ReplaceTypes(template, expr.field as (*Node))
            ReplaceTypes(template, expr.value)
            break;
        }
        case StmtKind.SIZE_OF_EXPR: {
            var expr = ast as (*SizeOfExpr)
            ReplaceTypes(template, expr.sizeOfExpr)
            break;
        }
        case StmtKind.STRING_EXPR: {
            break;
        }
        case StmtKind.SUBSCRIPT_GET_EXPR: {
            var expr = ast as (*SubscriptGetExpr)
            ReplaceTypes(template, expr.object)
            ReplaceTypes(template, expr.index)
            break;
        }
        case StmtKind.SUBSCRIPT_SET_EXPR: {
            var expr = ast as (*SubscriptSetExpr)
            ReplaceTypes(template, expr.object)
            ReplaceTypes(template, expr.index)
            ReplaceTypes(template, expr.value)
            break;
        }
        case StmtKind.TERNARY_EXPR: {
            var expr = ast as (*TernaryExpr)
            ReplaceTypes(template, expr.cond)
            ReplaceTypes(template, expr.then)
            ReplaceTypes(template, expr.other)
            break;
        }
        case StmtKind.TYPE_IDENTIFIER_EXPR: {
            var expr = ast as (*TypeIdentifierExpr)
            expr.type = template.replaceTypeSpec(expr.type)
            break;
        }
        case StmtKind.TYPE_OF_EXPR: {
            var expr = ast as (*TypeOfExpr)
            ReplaceTypes(template, expr.typeOfExpr)
            expr.type = template.replaceTypeSpec(expr.type)
            break;
        }
        case StmtKind.UNARY_EXPR: {
            var expr = ast as (*UnaryExpr)
            ReplaceTypes(template, expr.unaryExpr)
            break;
        }
        case StmtKind.POISON_EXPR: {
            break;
        }
        default: {
            printf("Statement Kind: %s\n", StmtKindAsStr(ast.kind))
            assert(false)
        }
    }

    return true
}

func HasGenericParam(checker: *TypeChecker, genericArgs: *Array<*TypeSpec>) : bool {
    for(var i = 0; i < genericArgs.size(); i += 1) {
        var arg = genericArgs.get(i)
        var name = arg.getBaseType()
        if(name && name.kind == TypeSpecKind.NAME) {
            if(checker.isGenericParamType(name)) {
                return true
            }
        }
    }

    return false
}