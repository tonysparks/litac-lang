import "std/mem" as mem;
import "std/io" as io;
import "std/assert";
import "std/string_view";
import "std/libc";

public struct StringBuffer {
    buffer: *char
    length: i32
    capacity: i32
}

public func CString(str: *char, len: i32 = -1) : StringBuffer {
    if(str == null) {
        return StringBuffer {
            .buffer = null,
            .length = 0,
            .capacity = 0
        }
    }

    if(len < 0) {
        len = strlen(str)
    }

    return StringBuffer {
        .buffer = str,
        .length = len,
        .capacity = len
    }
}

public func StringBufferInit(str: *char, capacity: i32, len: i32 = -1) : StringBuffer {
    if(str == null) {
        return StringBuffer {
            .buffer = null,
            .length = 0,
            .capacity = 0
        }
    }

    if(len < 0) {
        len = strlen(str)
    }

    if(len > capacity) {
        len = capacity
    }

    return StringBuffer {
        .buffer = str,
        .length = len,
        .capacity = capacity
    }
}


public func (s: *StringBuffer) hash() : u32 {
    return StringHashLen(s.buffer, s.length)
}

public func (s: *StringBuffer) equals(other: *StringBuffer) : bool {
    if(s == null || other == null) {
        return false
    }
    if(s.length != other.length) {
        return false
    }

    return strncmp(s.buffer, other.buffer, s.length) == 0
}

public func (s: *StringBuffer) format(format: *const char, ...) : i32{
    // get the variable args
    var args:va_list;
    va_start(args, format);

    // have to call vsnprintf at least once - might as well use a small
    // fixed-size buffer just in case the final string fits in it
    var len = vsnprintf(s.buffer, s.capacity, format, args);
    va_end(args);

    if(len < 0) {
        return len
    }

    s.length = len
    return len
}

public func (s: *StringBuffer) append(format: *const char, ...) : i32 {
    // get the variable args
    var args1:va_list;
    va_start(args1, format);

    // have to call vsnprintf at least once - might as well use a small
    // fixed-size buffer just in case the final string fits in it
    var len = vsnprintf(s.buffer + s.length, s.capacity - s.length, format, args1);
    va_end(args1);

    if(len < 0) {
        return len
    }

    s.length += len
    return len
}

public func (b: *StringBuffer) appendStr(str: *const char) {
    if(str == null) {
        return;
    }
    var len = strlen(str)
    b.appendStrn(str, len)
}


public func (b: *StringBuffer) appendStrn(str: *const char, len: i32) {
    if(str == null || len < 1) {
        return;
    }

    if(b.length + len > b.capacity) {
        len = b.capacity - b.length
    }

    memcpy((&b.buffer[b.length]) as (*void),
             str as (*const void),
             len * sizeof(:char))
    b.length += len
}


public func (s: *StringBuffer) setStrn(str: *const char, len: i32) {
    if(str == null || len < 1) {
        return;
    }

    if(len > s.capacity) {
        len = s.capacity
    }

    memcpy((&s.buffer[0]) as (*void),
             str as (*const void),
             len * sizeof(:char))
    s.length = len
}

public func (s: *StringBuffer) appendChar(c: char) {
    if(s.length >= s.capacity) {
        return;
    }

    s.buffer[s.length] = c
    s.length += 1
}

public func (s: StringBuffer) substring(start: i32, end: i32) : StringView {
    if(start < 0 || end < start) {
        return StringView{
            .buffer = null,
            .length = 0
        }
    }

    var len = end - start
    return StringView {
        .buffer = s.buffer + start,
        .length = len
    }
}

public func (s: StringBuffer) asView() : StringView {
    return StringView {
        .buffer = s.buffer,
        .length = s.length
    }
}

public func (s: StringBuffer) memset(value: i32 = 0) {
    if(s.capacity < 1) return;

    memset(s.buffer as (*void), value, s.capacity * sizeof(:char))
}


public func (s: StringBuffer) toLower() {
    for(var i = 0; i < s.length; i+=1) {
        s.buffer[i] = tolower(s.buffer[i]) as (char)
    }
}

public func (s: StringBuffer) toUpper() {
    for(var i = 0; i < s.length; i+=1) {
        s.buffer[i] = toupper(s.buffer[i]) as (char)
    }
}

public func (s: StringBuffer) copy(allocator: *const mem::Allocator = mem::defaultAllocator) : StringBuffer {
    if(s.length < 1) {
        return StringBuffer {
            .buffer = null,
            .length = 0,
            .capacity = 0
        }
    }

    var copiedStr = allocator.alloc((s.length + 1) * sizeof(:char)) as (*char)
    memcpy(copiedStr, s.buffer, s.length)

    copiedStr[s.length] = '\0'

    return StringBuffer{
        .buffer = copiedStr,
        .length = s.length,
        .capacity = s.length
    }
}

public func (s: *StringBuffer) adjust() {
    s.length = strnlen(s.buffer, s.capacity)
}

public func (s: *StringBuffer) clear() {
    s.memset(0)
    s.length = 0
}

public func (s: StringBuffer) empty() : bool {
    return s.length < 1
}

public func (s: StringBuffer) size() : i32 {
    return s.length
}

public func (s: StringBuffer) remaining() : i32 {
    return s.capacity - s.length
}

public func (s: StringBuffer) cStr() : *char {
    if(s.length < s.capacity && s.length > 0) {
        s.buffer[s.length] = '\0'
    }
    return s.buffer
}

public func (s: StringBuffer) cStrConst() : *const char {
    if(s.length < s.capacity && s.length > 0) {
        s.buffer[s.length] = '\0'
    }
    return s.buffer as (*const char)
}

public func StringClone(original: *const char, len: i32 = -1, allocator: *const mem::Allocator = mem::defaultAllocator) : *char {
    if(!original) {
        return null
    }

    if(len < 0) {
        len = strlen(original)
    }

    var copiedStr: *char = null

    if(len > 0) {
        copiedStr = allocator.alloc((len + 1) * sizeof(:char)) as (*char)
        memcpy(copiedStr, original, len)

        copiedStr[len] = '\0'
    }
    return copiedStr
}

public func StringCopy(src: *const char, dest: *char, size: usize) : *char {
    if(!src) {
        return null
    }

    if (size > 0) {
        var i:usize;
        for (i = 0; i < size - 1 && src[i]; i+=1) {
             dest[i] = src[i];
        }
        dest[i] = '\0';
    }
    return dest;
}

public func StringHashLen(str: *const char, size: usize) : u32 {
    var hash = 5381_u32;

    var index = 0
    while (index < size) {
        hash = ((hash << 5_u32) + hash) ^ ((*str) as (u32));
        str   += 1;
        index += 1;
    }
    return hash;
}

public func StringHash(str: *const char) : u32 {
    var hash = 5381_u32;

    while (*str) {
        hash = ((hash << 5_u32) + hash) ^ ((*str) as (u32));
        str += 1;
    }
    return hash;
}

public func StringEqualLen(a: *const char, b: *const char, size: usize) : bool {
    if(!a && !b) return true
    if(!a || !b) return false

    return strncmp(a, b, size) == 0
}

public func StringEqual(a: *const char, b: *const char) : bool {
    if(!a && !b) return true
    if(!a || !b) return false

    return strcmp(a, b) == 0
}

public func (str: *const char) hash() : u32 {
    return StringHash(str)
}

public func (str: *const char) equals(other: *const char) : bool {
    if(!str && !other) return true
    if(!str || !other) return false

    return strcmp(str, other) == 0
}
