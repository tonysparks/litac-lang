
fn getColumnName(field) {
    if(field.notes) {
        for(note in field.notes) {
            if(note.name == "column") {
                for(arg in note.arguments) {
                    if(arg.name == "name") {
                        return arg.value
                    }
                }
            }
        }
    }
    return field.name
}

fn getIdName(sym, fields) {
    // TODO error if no @id ??
    if(fields == null) {
        error(`No fields defined for ${sym.name}`)
        return null
    }

    var idField = null
    for(field in fields) {
        if(field.notes) {
            for(note in field.notes) {
                if(note.name == "id") {
                    idField = field
                    break
                }
            }
        }
    }

    if(idField == null) {
        error(`No @id field defined for ${sym.name}`)
        return null
    }

    return getColumnName(idField)
}


fn getTableName(sym) {
    var tableName = sym.name
    if(sym.notes) {
        for(note in sym.notes) {
            if(note.name == "table") {
                for(arg in note.arguments) {
                    if(arg.name == "name") {
                        return arg.value
                    }
                }
            }
        }
    }
    return tableName
}


fn EmitDBFunctions(sym) {
    if(sym.kind != "STRUCT_DECL") {
        return;
    }

    var structDecl = sym.structDecl

    var tableName = getTableName(sym)
    var idName = getIdName(sym, structDecl.fields)

    // INSERT
    {
        emit(
            `
            public func (this: *${sym.name}) getSqlInsert(): String {
                return
                    $"""
                    INSERT INTO ${tableName}
                    (`
        )
        emit(`\n`)
        var isFirst = true
        for(field in structDecl.fields) {
            if(!isFirst) {
                emit(`,\n`)
            }
            emit(`                        ${getColumnName(field)}`)
            isFirst = false
        }
        emit(`\n                    ) VALUES (\n`)

        isFirst = true
        for(field in structDecl.fields) {
            if(!isFirst) {
                emit(`,\n`)
            }
            emit(`                        :${getColumnName(field)}`)
            isFirst = false
        }
        emit(`\n                    );"""`)
        emit(
            `
            }
            `
        )
    }

    // SELECT ONE
    {
        emit(
            `
            public func (this: *${sym.name}) getSqlSelectOneById(): String {
                return
                    $"""
                    SELECT`
        )
        emit(`\n`)
        var isFirst = true
        for(field in structDecl.fields) {
            if(!isFirst) {
                emit(`,\n`)
            }
            emit(`                        ${getColumnName(field)}`)
            isFirst = false
        }
        emit(`\n                    FROM ${tableName} tmp`)
        emit(`\n                    WHERE tmp.${idName} = :id;"""`)
        emit(
            `
            }
            `
        )
    }

   return true
}

fn DBSymbols(sym) {
   if(sym.kind == "STRUCT_DECL" || sym.kind == "ENUM_DECL") {
      return EmitDBFunctions(sym)
   }
   return false
}

var symbols = getSymbolsByNote("table")
for(sym in symbols) {
   emitClear()

   if(DBSymbols(sym)) {
      print(emitStr())
      addDeclaration(sym.moduleFilename, emitStr())
   }
}
