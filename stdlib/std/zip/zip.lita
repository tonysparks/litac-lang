import "std/zip/miniz"
import "std/mem"
import "std/libc"
import "std/assert"
import "std/io"
import "std/system"
import "std/string/buffer"
import "std/string"
import "std/string/builder"
import "std/array"
import "std/fs"
import "std/limits"

@doc("""
    Open for either writing or reading
""")
public enum ZipOpen {
    READ,
    WRITE,
}

public enum ZipFlags {
    INCLUDE_ZIPFILE_NAME_ON_UNZIP = (1<<0),
}

@asStr
public enum ZipStatus {
    OK = 0,
    ERROR_UNABLE_TO_OPEN_FILE,
    ERROR_UNABLE_TO_STAT,
    ERROR_UNABLE_CREATE_DIR,
    ERROR_UNABLE_WRITE_FILE,
    ERROR_ADDING_FILE,
    ERROR_CLOSING,
    ERROR_FINALIZING,

    ERROR_OUT_OF_MEMORY,
    ERROR_COMPRESSING,
    ERROR_DECOMPRESSING,
}

public enum ZipCompressionLevel {
    NO_COMPRESSION = 0,
    BEST_SPEED = 1,
    BEST_COMPRESSION = 9,
    UBER_COMPRESSION = 10,
    DEFAULT_LEVEL = 6,
}


public func ZipCompress(buffer: *StringBuilder, source: String, level: ZipCompressionLevel) : ZipStatus {
    var maxLength = mz_compressBound(source.length)
    assert(maxLength < MAX_I32) // TODO: Make string types usize

    if(!buffer.reserve(maxLength as (i32))) {
        return ZipStatus.ERROR_OUT_OF_MEMORY
    }

    var length = buffer.remaining() as (u64);
    var status = mz_compress2(
        &buffer.buffer[buffer.length],
        &length,
        source.buffer,
        source.length,
        level as (i32)
    )

    if(status != MZ_OK) {
        return ZipStatus.ERROR_COMPRESSING
    }

    if (length > 0) {
        assert(length < MAX_I32) // TODO: Make string types usize
        buffer.length += length as (i32)
    }

    return ZipStatus.OK
}

public func ZipUncompress(buffer: *StringBuilder, source: String) : ZipStatus {

    var length = buffer.remaining() as (u64);
    var estimatedLength = MAX(source.length * 3, length as (i32))

    var status = MZ_OK
    var sourceLength = source.length as (u64)

    do {
        if(!buffer.reserve(estimatedLength)) {
            return ZipStatus.ERROR_OUT_OF_MEMORY
        }

        var bufferLength = buffer.remaining() as (u64);

        status = mz_uncompress2(
            &buffer.buffer[buffer.length],
            &bufferLength,
            source.buffer,
            &sourceLength
        )

        if(status == MZ_OK) {
            length = bufferLength
            break
        }

        estimatedLength *= 2;
    } while(status == MZ_BUF_ERROR);

    if(status != MZ_OK) {
        return ZipStatus.ERROR_DECOMPRESSING
    }

    if (length > 0) {
        assert(length < MAX_I32) // TODO: Make string types usize
        buffer.length += length as (i32)
    }

    return ZipStatus.OK
}

public struct ZipFile {
    archive: mz_zip_archive
    type: ZipOpen
    allocator: *const Allocator
}

func ZipFileAlloc(opaque: *void, items: usize, size: usize) : *void {
    var allocator = opaque as (*const Allocator);
    if(!allocator) {
        return null
    }

    return allocator.alloc(items * size)
}

func ZipFileReAlloc(opaque: *void, address: *void, old_items: usize, old_size: usize, items: usize, size: usize) : *void {
    var allocator = opaque as (*const Allocator);
    if(!allocator) {
        return null
    }

    return allocator.realloc(address, old_items * old_size, items * size)
}

func ZipFileFree(opaque: *void, address: *void) {
    var allocator = opaque as (*const Allocator);
    if(!allocator) {
        return;
    }

    allocator.free(address)
}

public func (this: *ZipFile) open(
    filename: *const char,
    type: ZipOpen,
    allocator: *const Allocator = defaultAllocator) : ZipStatus {

    memset(&this.archive, 0, sizeof(:mz_zip_archive));

    this.type = type
    this.allocator = allocator

    this.archive.m_pAlloc = ZipFileAlloc;
    this.archive.m_pFree = ZipFileFree;
    this.archive.m_pRealloc = ZipFileReAlloc;
    this.archive.m_pAlloc_opaque = allocator as (*void);

    if(type == ZipOpen.READ) {
        var status = mz_zip_reader_init_file(&this.archive, filename, .flags = 0);
        if(!status) {
            return ZipStatus.ERROR_UNABLE_TO_OPEN_FILE
        }
    }
    else {
        var status = mz_zip_writer_init_file(&this.archive, filename, 0)
        if(!status) {
            return ZipStatus.ERROR_UNABLE_TO_OPEN_FILE
        }
    }

    return ZipStatus.OK
}

public func (this: *ZipFile) close() : ZipStatus {
    if(this.type == ZipOpen.WRITE) {
        if(!mz_zip_writer_end(&this.archive)) {
            return ZipStatus.ERROR_CLOSING
        }
        return ZipStatus.OK
    }

    if(!mz_zip_end(&this.archive)) {
        return ZipStatus.ERROR_CLOSING
    }
    return ZipStatus.OK
}


@doc("
    When writing to the zip file, finalize must be called to mark
    the completion of writing.
")
public func (this: *ZipFile) finalize() : ZipStatus {
    if(!mz_zip_writer_finalize_archive(&this.archive)) {
        return ZipStatus.ERROR_FINALIZING
    }
    return ZipStatus.OK
}

public func (this: *ZipFile) isValid() : bool {
    return mz_zip_validate_archive(&this.archive, 0)
}

public func (this: *ZipFile) getLastError() : *const char {
    return mz_zip_get_error_string(this.archive.m_last_error)
}

public func (this: *ZipFile) zipDir(
    pathToZip: *const char,
    len: i32,
    baseName: *const char,
    level: ZipCompressionLevel = ZipCompressionLevel.DEFAULT_LEVEL) : ZipStatus {

    var basePath = StringInit(pathToZip, len)

    var tempArch:[MAX_PATH]char;
    var tempPath:[MAX_PATH]char;

    var archiveName = StringBufferInit(tempArch, MAX_PATH, 0)
    var path = StringBufferInit(tempPath, MAX_PATH, 0)
    path.format("%.*s", basePath.length, basePath.buffer)

    return this.zipFolder(baseName, &basePath, &archiveName, &path, level)
}

func (this: *ZipFile) zipFolder(
    baseName: *const char,
    basePath: *String,
    archiveName: *StringBuffer,
    path: *StringBuffer,
    level: ZipCompressionLevel) : ZipStatus {

    var handle: FileHandle{}
    if(!handle.openEx(path.buffer, path.length)) {
        return ZipStatus.ERROR_UNABLE_TO_OPEN_FILE
    }
    defer handle.close()

    while(handle.next()) {
        if(handle.isDirectory()) {
            var len = path.length
            path.append("/%s", handle.name())

            var status = this.zipFolder(baseName, basePath, archiveName, path, level)
            path.length = len

            if(status != ZipStatus.OK) {
                return status
            }
        }
        else {
            archiveName.format("%s%.*s/%s",
                baseName,
                path.length - basePath.length,
                path.buffer + basePath.length,
                handle.name())

            var len = path.length
            path.append("/%s", handle.name())

            if(this.addFile(archiveName.cStr(), path.cStr(), level) != ZipStatus.OK) {
                return ZipStatus.ERROR_ADDING_FILE
            }
            path.length = len
        }
    }

    return ZipStatus.OK
}


// TODO: Expose compression levels
public func (this: *ZipFile) addFile(
    archiveName: *const char,
    filename: *const char,
    level: ZipCompressionLevel = ZipCompressionLevel.DEFAULT_LEVEL) : ZipStatus {

    if(!mz_zip_writer_add_file(
            &this.archive,
            archiveName,
            filename,
            "",
            0_u16/*,
            level as (CompressionStrategy)*/)) {
        return ZipStatus.ERROR_ADDING_FILE
    }

    return ZipStatus.OK
}

@doc("""
    dest - the destination folder to write the zipped files to
    extractedFiles - optional parameter to store the unzipped file names, if this is supplied
        than a copy of the string is stored in this Array.  It is up to the caller of this function
        to manage the memory.  The allocation uses the allocator used in ZipFile.open.
    flags - optional parameter, @see ZipFlags
""")
public func (this: *ZipFile) unzip(
    dest: *const char,
    extractedFiles: *Array<*const char> = null,
    flags: i32 = 0) : ZipStatus {

    var fileCount = mz_zip_reader_get_num_files(&this.archive)
    if(fileCount == 0) {
        return ZipStatus.OK
    }

    // Get the base filename, so that we can remove
    // it from the output path if requested
    var fileStat: mz_zip_archive_file_stat;
    if(!mz_zip_reader_file_stat(&this.archive, 0, &fileStat)) {
        return ZipStatus.ERROR_UNABLE_TO_STAT
    }

    var temp:[MAX_PATH]char;
    var temp2:[MAX_PATH]char;
    var path = StringBufferInit(temp, MAX_PATH, 0)

    var baseLen = strlen(fileStat.m_filename);
    if( (flags | ZipFlags.INCLUDE_ZIPFILE_NAME_ON_UNZIP) > 0) {
        baseLen = 0
    }

    for(var i = 0; i < fileCount; i+=1) {
        if(!mz_zip_reader_file_stat(&this.archive, i, &fileStat)) {
            continue
        }

        var filename = StringBufferInit(fileStat.m_filename + baseLen, MZ_ZIP_MAX_ARCHIVE_FILENAME_SIZE - baseLen, -1)
        path.format("%s/%.*s", dest, filename.length, filename.buffer)
        var cPath = path.cStr()

        Mkdirs(FileParent(cPath, temp2))

        if(mz_zip_reader_is_file_a_directory(&this.archive, i)) {
            Mkdir(cPath)
            if(!FileExists(cPath)) {
                return ZipStatus.ERROR_UNABLE_CREATE_DIR
            }
        }
        else if(!mz_zip_reader_extract_to_file(&this.archive, i, cPath, 0)) {
            return ZipStatus.ERROR_UNABLE_WRITE_FILE
        }

        if(extractedFiles) {
            extractedFiles.add(StringClone(filename.buffer, filename.length, this.allocator))
        }
    }

    return ZipStatus.OK
}

/*
@test
func testUnzip() {
    var zip: ZipFile{}
    defer zip.close()

    assert(zip.open("C:/Users/antho/Downloads/seventh-0.4.1-beta.zip", ZipOpen.READ) == ZipStatus.OK)
    assert(zip.unzip("C:/Users/antho/Desktop/temp") == ZipStatus.OK)
}*/


import "std/mem/linear_allocator"
/*
@test
func testZipDir() {
    var zip: ZipFile{}
    defer zip.close()

    const size = 128*MiB
    var mem = defaultAllocator.alloc(size)
    var alloc = LinearAllocator{}
    alloc.init(mem, size)

    assert(zip.open("test.zip", ZipOpen.WRITE, alloc.allocator) == ZipStatus.OK)
    assert(zip.zipDir("C:/Users/antho/git/c2litac", strlen("C:/Users/antho/git/c2litac"), "pkg") == ZipStatus.OK)
    assert(zip.finalize() == ZipStatus.OK)
}*/


//@test
func testZipDir() {
    var zip: ZipFile{}
    defer zip.close()

    assert(zip.open("test.zip", ZipOpen.WRITE) == ZipStatus.OK)
    //assert(zip.addFile("pkg/pkg.json", "C:/Users/antho/git/c2litacx/pkg.json") == ZipStatus.OK)
    //assert(zip.addFile("pkg/src/test.txt", "C:/Users/antho/git/c2litacx/src/test.txt") == ZipStatus.OK)

    var path = "/home/tony/projects/libio_uring"
    var len = path.length()
    assert(zip.zipDir(path, len, "pkg") == ZipStatus.OK)
    assert(zip.finalize() == ZipStatus.OK)
}

@test
func testGZip() {
    var compressed = StringBuilderInit(1024)
    var input =
    """
        !#$%&'()*+,-./0123456789:;<=>?@ABCDEFGHIJKLMNOPQRSTUVWXYZ[]^_`abcdefghijklmnopqrstuvwxyz{|}~ !#$%&'()*+,-./0123456789:;<=>?@ABCDEFGHIJKLMNOPQRSTUVWXYZ[]^_`abcdefghijklmnopqrstuvwxyz{|}~ !#$%&'()*+,-./0123456789:;<=>?@ABCDEFGHIJKLMNOPQRSTUVWXYZ[]^_`abcdefghijklmnopqrstuvwxyz{|}~ !#$%&'()*+,-./0123456789:;<=>?@ABCDEFGHIJKLMNOPQRSTUVWXYZ[]^_`abcdefghijklmnopqrstuvwxyz{|}~ !#$%&'()*+,-./0123456789:;<=>?@ABCDEFGHIJKLMNOPQRSTUVWXYZ[]^_`abcdefghijklmnopqrstuvwxyz{|}~ !#$%&'()*+,-./0123456789:;<=>?@ABCDEFGHIJKLMNOPQRSTUVWXYZ[]^_`abcdefghijklmnopqrstuvwxyz{|}~ !#$%&'()*+,-./0123456789:;<=>?@ABCDEFGHIJKLMNOPQRSTUVWXYZ[]^_`abcdefghijklmnopqrstuvwxyz{|}~ !#$%&'()*+,-./0123456789:;<=>?@ABCDEFGHIJKLMNOPQRSTUVWXYZ[]^_`abcdefghijklmnopqrstuvwxyz{|}~ !#$%&'()*+,-./0123456789:;<=>?@ABCDEFGHIJKLMNOPQRSTUVWXYZ[]^_`abcdefghijklmnopqrstuvwxyz{|}~ !#$%&'()*+,-./0123456789:;<=>?@ABCDEFGHIJKLMNOPQRSTUVWXYZ[]^_`abcdefghijklmnopqrstuvwxyz{|}~ !#$%&'()*+,-./0123456789:;<=>?@ABCDEFGHIJKLMNOPQRSTUVWXYZ[]^_`abcdefghijklmnopqrstuvwxyz{|}~ !#$%&'()*+,-./0123456789:;<=>?@ABCDEFGHIJKLMNOPQRSTUVWXYZ[]^_`abcdefghijklmnopqrstuvwxyz{|}~ !#$%&'()*+,-./0123456789:;<=>?@ABCDEFGHIJKLMNOPQRSTUVWXYZ[]^_`abcdefghijklmnopqrstuvwxyz{|}~ !#$%&'()*+,-./0123456789:;<=>?@ABCDEFGHIJKLMNOPQRSTUVWXYZ[]^_`abcdefghijklmnopqrstuvwxyz{|}~ !#$%&'()*+,-./0123456789:;<=>?@ABCDEFGHIJKLMNOPQRSTUVWXYZ[]^_`abcdefghijklmnopqrstuvwxyz{|}~ !#$%&'()*+,-./0123456789:;<=>?@ABCDEFGHIJKLMNOPQRSTUVWXYZ[]^_`abcdefghijklmnopqrstuvwxyz{|}~ !#$%&'()*+,-./0123456789:;<=>?@ABCDEFGHIJKLMNOPQRSTUVWXYZ[]^_`abcdefghijklmnopqrstuvwxyz{|}~ !#$%&'()*+,-./0123456789:;<=>?@ABCDEFGHIJKLMNOPQRSTUVWXYZ[]^_`abcdefghijklmnopqrstuvwxyz{|}~ !#$%&'()*+,-./0123456789:;<=>?@ABCDEFGHIJKLMNOPQRSTUVWXYZ[]^_`abcdefghijklmnopqrstuvwxyz{|}~ !#$%&'()*+,-./0123456789:;<=>?@ABCDEFGHIJKLMNOPQRSTUVWXYZ[]^_`abcdefghijklmnopqrstuvwxyz{|}~ !#$%&'()*+,-./0123456789:;<=>?@ABCDEFGHIJKLMNOPQRSTUVWXYZ[]^_`abcdefghijklmnopqrstuvwxyz{|}~ !#$%&'()*+,-./0123456789:;<=>?@ABCDEFGHIJKLMNOPQRST
    """.toString()
    var compressStatus = ZipCompress(
        &compressed,
        input,
        6
    )
    assert(compressStatus == ZipStatus.OK)

    printf("CompressedSize: %d\n", compressed.length)

    var uncompressed = StringBuilderInit(1024 * 1)
    var uncompressStatus = ZipUncompress(
        &uncompressed,
        compressed.toString()
    )
    printf("Status: %s\n", ZipStatusAsStr(uncompressStatus))
    assert(uncompressStatus == ZipStatus.OK)
    assert(uncompressed.toString().equals(input))
}